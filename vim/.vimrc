
"~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
" Vimrc: configuration for vim and neovim.
"        set 'Global settings' before using this vimrc.
" Github: https://github.com/yehuohan/dotconfigs
" Author: yehuohan, <yehuohan@qq.com>, <yehuohan@gmail.com>
"~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

" README {{{
" Help {{{
    " help/h        : æŸ¥çœ‹Vimå¸®åŠ©
    " <S-k>         : å¿«é€ŸæŸ¥çœ‹å…‰æ ‡æ‰€åœ¨cwordæˆ–é€‰æ‹©å†…å®¹çš„vimå¸®åŠ©
    " h *@en        : æŒ‡å®šæŸ¥çœ‹è‹±æ–‡(enï¼Œcnå³ä¸ºä¸­æ–‡)å¸®åŠ©
    " h index       : å¸®åŠ©åˆ—è¡¨
    " h range       : CommandèŒƒå›´
    " h pattern     : åŒ¹é…æ¨¡å¼
    " h magic       : MagicåŒ¹é…æ¨¡å¼
    " h Visual      : Visualæ¨¡å¼
    " h map-listing : æ˜ å°„å‘½ä»¤
    " h registers   : å¯„å­˜å™¨åˆ—è¡¨
" }}}

" Map {{{
    " - Normalæ¨¡å¼ä¸‹ä½¿ç”¨<leader>ä»£æ›¿<C-?>,<S-?>,<A-?>ï¼Œ
    " - Insertæ¨¡å¼ä¸‹mapå¸¦ctrl,altçš„å¿«æ·é”®
    " - å°½é‡ä¸æ”¹å˜vimåŸæœ‰é”®ä½çš„åŠŸèƒ½å®šä¹‰
    " - å°½é‡ä¸€åªæ‰‹ä¸åŒæ—¶æŒ‰ä¸¤ä¸ªé”®ï¼Œä¸”è¿ç»­æŒ‰é”®ç›¸éš”å°½é‡è¿‘
    " - å°½é‡ä¸æ˜ å°„åè¿œçš„æŒ‰é”®ï¼ˆF1~F12ï¼Œæ•°å­—é”®ç­‰ï¼‰ï¼Œä¸”é›†ä¸­äº'j,k,i,o'é”®ä½é™„è¿‘
    " - è°ƒæ¢Escå’ŒCapsLocké”®
" }}}
" }}} End

" Platform {{{
" vim or nvim, with or without gui {{{
function! IsVim()
    return !(has('nvim'))
endfunction
function! IsNVim()
    return (has('nvim'))
endfunction
function! IsGVim()
    return has('gui_running')
endfunction
function! IsNVimQt()
    " åªåœ¨VimEnterä¹‹åèµ·ä½œç”¨
    return exists('g:GuiLoaded')
endfunction
" }}}

" linux or win {{{
function! IsLinux()
    return (has('unix') && !has('macunix') && !has('win32unix'))
endfunction
function! IsWin()
    return (has('win32') || has('win64'))
endfunction
function! IsGw()
    " GNU for windows
    return (has('win32unix'))
endfunction
function! IsMac()
    return (has('mac'))
endfunction
" }}}

" term {{{
function! IsTermType(tt)
    return (&term ==? a:tt) ? 1 : 0
endfunction
" }}}
" }}} End

" Global Settings {{{
let s:home = resolve(expand('<sfile>:p:h'))
if (IsLinux() || IsMac() || IsGw())
    let $DotVimPath=s:home . '/.vim'
elseif IsWin()
    let $DotVimPath=s:home . '\vimfiles'
endif
set rtp+=$DotVimPath

" first {{{
set encoding=utf-8                      " å†…éƒ¨ä½¿ç”¨utf-8ç¼–ç 
if IsVim()
    set nocompatible                    " ä¸å…¼å®¹vi
endif
let mapleader="\<Space>"
nnoremap ; :
nnoremap : ;
vnoremap ; :
set timeout                             " æ‰“å¼€æ˜ å°„è¶…æ—¶æ£€æµ‹
set ttimeout                            " æ‰“å¼€é”®ç è¶…æ—¶æ£€æµ‹
set timeoutlen=1000                     " æ˜ å°„è¶…æ—¶æ—¶é—´ä¸º1000ms
set ttimeoutlen=70                      " é”®ç è¶…æ—¶æ—¶é—´ä¸º70ms
if IsVim()
    " ç»ˆç«¯Alté”®æ˜ å°„å¤„ç†ï¼šå¦‚ Alt+xï¼Œå®é™…è¿ç»­å‘é€ <Esc>x ç¼–ç 
    " ä»¥ä¸‹ä¸‰ç§æ–¹æ³•éƒ½å¯ä»¥ä½¿æŒ‰ä¸‹ Alt+x åï¼Œæ‰§è¡Œ CmdTest å‘½ä»¤ï¼Œä½†è¶…æ—¶æ£€æµ‹æœ‰åŒºåˆ«
    "<1> set <M-x>=x  " è®¾ç½®é”®ç ï¼Œè¿™é‡Œçš„æ˜¯ä¸€ä¸ªå­—ç¬¦ï¼Œå³<Esc>çš„ç¼–ç ï¼Œä¸æ˜¯^å’Œ[æ”¾åœ¨ä¸€èµ·
                        " åœ¨ç»ˆç«¯çš„Insertæ¨¡å¼ï¼ŒæŒ‰Ctrl+vå†æŒ‰Alt+xå¯è¾“å…¥
    "    nnoremap <M-x> :CmdTest<CR>    " æŒ‰é”®ç è¶…æ—¶æ—¶é—´æ£€æµ‹
    "<2> nnoremap <Esc>x :CmdTest<CR>   " æŒ‰æ˜ å°„è¶…æ—¶æ—¶é—´æ£€æµ‹
    "<3> nnoremap x  :CmdTest<CR>     " æŒ‰æ˜ å°„è¶…æ—¶æ—¶é—´æ£€æµ‹
    for t in split('q w e r t y u i o p a s d f g h j k l z x c v b n m', ' ')
        execute 'set <M-'. t . '>=' . t
    endfor
    set <M-,>=,
    set <M-.>=.
    set <M-;>=;
endif
" }}}

" s:gset {{{
let s:gset_file = $DotVimPath . '/.gset.json'
let s:gset = {
    \ 'set_dev' : v:null,
    \ 'set_os' : v:null,
    \ 'use_powerfont' : 1,
    \ 'use_lightline': 1,
    \ 'use_startify' : 1,
    \ 'use_fzf' : 1,
    \ 'use_leaderf' : 1,
    \ 'use_ycm' : 1,
    \ 'use_ultisnips' : 1,
    \ 'use_lcn' : 0,
    \ 'use_utils' : 1,
    \ }
" FUNCTION: s:gsetLoad() {{{
function! s:gsetLoad()
    if filereadable(s:gset_file)
        call extend(s:gset, json_decode(join(readfile(s:gset_file))), 'force')
    else
        call s:gsetSave()
    endif
    call env#env(s:gset.set_dev, s:gset.set_os)
endfunction
" }}}
" FUNCTION: s:gsetSave() {{{
function! s:gsetSave()
    call writefile([json_encode(s:gset)], s:gset_file)
    echo 's:gset save successful!'
endfunction
" }}}
" FUNCTION: s:gsetInit() {{{
function! s:gsetInit()
    for [key, val] in sort(items(s:gset))
        let s:gset[key] = input('let s:gset.'. key . ' = ', val)
    endfor
    redraw
    call s:gsetSave()
endfunction
" }}}
" FUNCTION: s:gsetShow() {{{
function! s:gsetShow()
    let l:str = 'Gset:'
    for [key, val] in sort(items(s:gset))
        let l:str .= "\n    " . key . ' = ' . val
    endfor
    echo l:str
endfunction
" }}}
command! -nargs=0 GSLoad :call s:gsetLoad()
command! -nargs=0 GSInit :call s:gsetInit()
command! -nargs=0 GSShow :call s:gsetShow()
call s:gsetLoad()
" }}}
" }}} End

" Plug Settings {{{
" s:plug {{{
let s:plug = {
    \ 'timer' : 0,
    \ 'delay' : 700,
    \ 'list'  : []
    \ }
" FUNCTION: s:plug.append(name) dict {{{
function! s:plug.append(name) dict
    call add(self.list, a:name)
endfunction
" }}}

" FUNCTION: s:plug.init() dict {{{
function! s:plug.init() dict
    if !empty(self.list)
        let s:plug.timer = timer_start(s:plug.delay, {timer -> call('plug#load', self.list)})
    endif
endfunction
" }}}
" }}}

call plug#begin($DotVimPath.'/bundle')  " å¯ä»¥æŒ‡å®šæ’ä»¶å®‰è£…ä½ç½®

" åŸºæœ¬ç¼–è¾‘ {{{
" easy-motion {{{ å¿«é€Ÿè·³è½¬
    Plug 'easymotion/vim-easymotion'
    let g:EasyMotion_do_mapping = 0     " ç¦æ­¢é»˜è®¤map
    let g:EasyMotion_smartcase = 1      " ä¸åŒºåˆ†å¤§å°å†™
    nmap s <Plug>(easymotion-overwin-f)
    nmap <leader>ms <Plug>(easymotion-overwin-f2)
                                        " è·¨åˆ†å±å¿«é€Ÿè·³è½¬åˆ°å­—æ¯
    nmap <leader>j <Plug>(easymotion-j)
    nmap <leader>k <Plug>(easymotion-k)
    nmap <leader>mw <Plug>(easymotion-w)
    nmap <leader>mb <Plug>(easymotion-b)
    nmap <leader>me <Plug>(easymotion-e)
    nmap <leader>mg <Plug>(easymotion-ge)
    nmap <leader>mW <Plug>(easymotion-W)
    nmap <leader>mB <Plug>(easymotion-B)
    nmap <leader>mE <Plug>(easymotion-E)
    nmap <leader>mG <Plug>(easymotion-gE)
" }}}

" vim-visual-multi {{{ å¤šå…‰æ ‡ç¼–è¾‘
    Plug 'mg979/vim-visual-multi'
    let g:VM_mouse_mappings = 0         " ç¦ç”¨é¼ æ ‡
    " C-n: è¿›å…¥cursoræ¨¡å¼
    " C-Up/Down: è¿›å…¥extendæ¨¡å¼
    " Tab: åˆ‡æ¢cursor/extendæ¨¡å¼
    let g:VM_leader = ','
    let g:VM_maps = {
        \ 'Find Under'         : '<C-n>',
        \ 'Find Subword Under' : '<C-n>',
        \ 'Select Cursor Down' : '<C-Down>',
        \ 'Select Cursor Up'   : '<C-Up>',
        \ 'Switch Mode'        : '<Tab>',
        \ }
    let g:VM_custom_remaps = {
        \ '<C-p>': '[',
        \ '<C-s>': 'q',
        \ '<C-c>': 'Q',
        \ }
" }}}

" textmanip {{{ å—ç¼–è¾‘
    Plug 't9md/vim-textmanip'
    let g:textmanip_enable_mappings = 0
    function! Plug_tm_setMode(mode)
        let g:textmanip_current_mode = a:mode
        echo 'textmanip mode: ' . g:textmanip_current_mode
    endfunction

    " åˆ‡æ¢Insert/Replace Mode
    xnoremap <M-i> :<C-U>call Plug_tm_setMode('insert')<CR>gv
    xnoremap <M-o> :<C-U>call Plug_tm_setMode('replace')<CR>gv
    " C-i ä¸ <Tab>ç­‰ä»·
    xnoremap <C-i> :<C-U>call Plug_tm_setMode('insert')<CR>gv
    xnoremap <C-o> :<C-U>call Plug_tm_setMode('replace')<CR>gv
    " æ›´æ®Modeä½¿ç”¨Move-Insertæˆ–Move-Replace
    xmap <C-j> <Plug>(textmanip-move-down)
    xmap <C-k> <Plug>(textmanip-move-up)
    xmap <C-h> <Plug>(textmanip-move-left)
    xmap <C-l> <Plug>(textmanip-move-right)
    " æ›´æ®Modeä½¿ç”¨Duplicate-Insertæˆ–Duplicate-Replace
    xmap <M-j> <Plug>(textmanip-duplicate-down)
    xmap <M-k> <Plug>(textmanip-duplicate-up)
    xmap <M-h> <Plug>(textmanip-duplicate-left)
    xmap <M-l> <Plug>(textmanip-duplicate-right)
"}}}

" traces {{{ é¢„è§ˆå¢å¼º
    Plug 'markonm/traces.vim'
    " æ”¯æŒ:s, :g, :v, :sort, :rangeé¢„è§ˆ
" }}}

" incsearch {{{ æŸ¥æ‰¾é¢„è§ˆ
    Plug 'haya14busa/incsearch.vim'
    Plug 'haya14busa/incsearch-fuzzy.vim'
    let g:incsearch#auto_nohlsearch = 1 " åœæ­¢æœç´¢æ—¶ï¼Œè‡ªåŠ¨å…³é—­é«˜äº®

    " è®¾ç½®æŸ¥æ‰¾æ—¶é¡µé¢æ»šåŠ¨æ˜ å°„
    augroup PluginIncsearch
        autocmd!
        autocmd VimEnter * call s:Plug_incs_keymap()
    augroup END
    function! s:Plug_incs_keymap()
        if exists('g:loaded_incsearch')
            IncSearchNoreMap <C-j> <Over>(incsearch-next)
            IncSearchNoreMap <C-k> <Over>(incsearch-prev)
            IncSearchNoreMap <M-j> <Over>(incsearch-scroll-f)
            IncSearchNoreMap <M-k> <Over>(incsearch-scroll-b)
        endif
    endfunction

    nmap /  <Plug>(incsearch-forward)
    nmap ?  <Plug>(incsearch-backward)
    nmap g/ <Plug>(incsearch-stay)
    nmap z/ <Plug>(incsearch-fuzzy-/)
    nmap z? <Plug>(incsearch-fuzzy-?)
    nmap zg/ <Plug>(incsearch-fuzzy-stay)
    nmap n  <Plug>(incsearch-nohl-n)
    nmap N  <Plug>(incsearch-nohl-N)
    " *,#ä½¿ç”¨\< \>ï¼Œè€Œg*,g# ä¸ä½¿ç”¨\< \>
    nmap *  <Plug>(incsearch-nohl-*)
    nmap #  <Plug>(incsearch-nohl-#)
    nmap g* <Plug>(incsearch-nohl-g*)
    nmap g# <Plug>(incsearch-nohl-g#)
    nmap <leader>8  <Plug>(incsearch-nohl-*)
    nmap <leader>3  <Plug>(incsearch-nohl-#)
    nmap <leader>g8 <Plug>(incsearch-nohl-g*)
    nmap <leader>g3 <Plug>(incsearch-nohl-g#)
" }}}

" fzf {{{ æ¨¡ç³ŠæŸ¥æ‰¾
if s:gset.use_fzf
    " linuxä¸‹ç›´æ¥pacman -S fzf
    " winä¸‹è½½fzf.exeæ”¾å…¥bundle/fzf/bin/ä¸‹
    if IsWin()
        Plug 'junegunn/fzf', {'on': ['FzfFiles', 'FzfRg']}
    endif
    Plug 'junegunn/fzf.vim', {'on': ['FzfFiles', 'FzfRg']}
    let g:fzf_command_prefix = 'Fzf'
    nnoremap <leader><leader>f :call feedkeys(':FzfFiles ', 'n')<CR>
endif
" }}}

" LeaderF {{{ æ¨¡ç³ŠæŸ¥æ‰¾
if s:gset.use_leaderf
if IsLinux()
    Plug 'Yggdroot/LeaderF', {'do': './install.sh'}
    augroup PluginLeaderF
        autocmd!
        autocmd VimEnter * call s:Plug_lf_removeMru()
    augroup END
    function! s:Plug_lf_removeMru()
        try
            autocmd! LeaderF_Mru
        " E216:äº‹ä»¶ç»„ä¸å­˜åœ¨
        catch /^Vim\%((\a\+)\)\=:E216/
        endtry
    endfunction
elseif IsWin()
    Plug 'Yggdroot/LeaderF', {'do': './install.bat'}
else
    Plug 'Yggdroot/LeaderF'
endif
    let g:Lf_CacheDirectory = $DotVimPath
if s:gset.use_powerfont
    let g:Lf_StlSeparator = {'left': 'î‚°', 'right': 'î‚²'}
else
    let g:Lf_StlSeparator = {'left': '', 'right': ''}
endif
    let g:Lf_ShortcutF = ''
    let g:Lf_ShortcutB = ''
    let g:Lf_ReverseOrder = 1
    let g:Lf_ShowHidden = 1             " æœç´¢éšè—æ–‡ä»¶å’Œç›®å½•
    nnoremap <leader><leader>l :call feedkeys(':LeaderfFile ', 'n')<CR>
    nnoremap <leader>lf :LeaderfFile<CR>
    nnoremap <leader>lu :LeaderfFunction<CR>
    nnoremap <leader>lU :LeaderfFunctionAll<CR>
    nnoremap <leader>ll :LeaderfLine<CR>
    nnoremap <leader>lL :LeaderfLineAll<CR>
    nnoremap <leader>lb :LeaderfBuffer<CR>
    nnoremap <leader>lB :LeaderfBufferAll<CR>
    nnoremap <leader>lr :LeaderfRgInteractive<CR>
    nnoremap <leader>lm :LeaderfMru<CR>
    nnoremap <leader>lM :LeaderfMruCwd<CR>
    nnoremap <leader>ls :LeaderfSelf<CR>
    nnoremap <leader>lh :LeaderfHelp<CR>
endif
" }}}

" grep {{{ å¤§èŒƒå›´æŸ¥æ‰¾
if IsVim()
    Plug 'yehuohan/grep'                " ä¸æ”¯æŒneovim
endif
    Plug 'mhinz/vim-grepper', {'on': ['Grepper', '<plug>(GrepperOperator)']}
    let g:grepper = {
        \ 'rg': {
            \ 'grepprg':    'rg -H --no-heading --vimgrep' . (has('win32') ? ' $*' : ''),
            \ 'grepformat': '%f:%l:%c:%m',
            \ 'escape':     '\^$.*+?()[]{}|'}
        \}
" }}}

" far {{{ æŸ¥æ‰¾ä¸æ›¿æ¢
    Plug 'brooth/far.vim', {'on': 'Farp'}
    let g:far#file_mask_favorites = ['%', '*.txt']
    nnoremap <leader>sr :Farp<CR>
                                        " Search and Replace, ä½¿ç”¨Fardoå’ŒFarundoæ¥æ›´æ”¹æ›¿æ¢ç»“æœ
    nnoremap <leader>fd :Fardo<CR>
    nnoremap <leader>fu :Farundo<CR>
" }}}

" tabular {{{ å­—ç¬¦å¯¹é½
    Plug 'godlygeek/tabular', {'on': 'Tabularize'}
    " /,/r2l0   -   ç¬¬1ä¸ªfieldä½¿ç”¨ç¬¬1ä¸ªå¯¹é½ç¬¦ï¼ˆå³å¯¹é½ï¼‰ï¼Œå†æ’å…¥2ä¸ªç©ºæ ¼
    "               ç¬¬2ä¸ªfieldä½¿ç”¨ç¬¬2ä¸ªå¯¹é½ç¬¦ï¼ˆå·¦å¯¹é½ï¼‰ï¼Œå†æ’å…¥0ä¸ªç©ºæ ¼
    "               ç¬¬3ä¸ªfieldåˆé‡æ–°ä»ç¬¬1ä¸ªå¯¹é½ç¬¦å¼€å§‹ï¼ˆå¯¹é½ç¬¦å¯ä»¥æœ‰å¤šä¸ªï¼Œå¾ªç¯ä½¿ç”¨ï¼‰
    "               è¿™æ ·å°±ç›¸å½“äºï¼šéœ€å¯¹é½çš„fieldä½¿ç”¨ç¬¬1ä¸ªå¯¹é½ç¬¦ï¼Œåˆ†å‰²ç¬¦(,)fieldä½¿ç”¨ç¬¬2ä¸ªå¯¹é½ç¬¦
    " /,\zs     -   å°†åˆ†å‰²ç¬¦(,)ä½œä¸ºå¯¹é½å†…å®¹fieldé‡Œçš„å­—ç¬¦
    nnoremap <leader><leader>a :call feedkeys(':Tabularize /', 'n')<CR>
    vnoremap <leader><leader>a :Tabularize /
" }}}

" easy-align {{{ å­—ç¬¦å¯¹é½
    Plug 'junegunn/vim-easy-align'
    " é»˜è®¤å¯¹é½å†…å«æ®µè½ï¼ˆText Object: vipï¼‰
    nmap <leader>ga <Plug>(EasyAlign)ip
    xmap <leader>ga <Plug>(EasyAlign)
    " å‘½ä»¤æ ¼å¼
    ":EasyAlign[!] [N-th]DELIMITER_KEY[OPTIONS]
    ":EasyAlign[!] [N-th]/REGEXP/[OPTIONS]
    nnoremap <leader><leader>g :call feedkeys(':' . join(GetContentRange('^[ \t]*$', '^[ \t]*$'), ',') . 'EasyAlign', 'n')<CR>
    vnoremap <leader><leader>g :EasyAlign
" }}}

" smooth-scroll {{{ å¹³æ»‘æ»šåŠ¨
    Plug 'terryma/vim-smooth-scroll'
    nnoremap <M-n> :call smooth_scroll#down(&scroll, 0, 2)<CR>
    nnoremap <M-m> :call smooth_scroll#up(&scroll, 0, 2)<CR>
    nnoremap <M-j> :call smooth_scroll#down(&scroll*2, 0, 4)<CR>
    nnoremap <M-k> :call smooth_scroll#up(&scroll*2, 0, 4)<CR>
" }}}

" expand-region {{{ å¿«é€Ÿå—é€‰æ‹©
    Plug 'terryma/vim-expand-region'
    nmap <C-p> <Plug>(expand_region_expand)
    vmap <C-p> <Plug>(expand_region_expand)
    nmap <C-u> <Plug>(expand_region_shrink)
    vmap <C-u> <Plug>(expand_region_shrink)
" }}}

" textobj-user {{{ æ–‡æœ¬å¯¹è±¡
    Plug 'kana/vim-textobj-user'
    Plug 'kana/vim-textobj-indent'
    let g:textobj_indent_no_default_key_mappings=1
    omap aI <Plug>(textobj-indent-a)
    omap iI <Plug>(textobj-indent-i)
    omap ai <Plug>(textobj-indent-same-a)
    omap ii <Plug>(textobj-indent-same-i)
    vmap aI <Plug>(textobj-indent-a)
    vmap iI <Plug>(textobj-indent-i)
    vmap ai <Plug>(textobj-indent-same-a)
    vmap ii <Plug>(textobj-indent-same-i)
    Plug 'kana/vim-textobj-function'
    Plug 'glts/vim-textobj-comment'
    Plug 'adriaanzon/vim-textobj-matchit'
    Plug 'lucapette/vim-textobj-underscore'
    omap au <Plug>(textobj-underscore-a)
    omap iu <Plug>(textobj-underscore-i)
    vmap au <Plug>(textobj-underscore-a)
    vmap iu <Plug>(textobj-underscore-i)
" }}}

" repeat {{{ é‡å¤å‘½ä»¤
    Plug 'tpope/vim-repeat'
    function! Plug_rpt_setExecution(string)
        let s:execution = a:string
        try
            call repeat#set("\<Plug>Plug_rpt_execute", v:count)
        " E117: å‡½æ•°ä¸å­˜åœ¨
        catch /^Vim\%((\a\+)\)\=:E117/
        endtry
    endfunction
    function! Plug_rpt_execute()
        if exists('s:execution') && !empty(s:execution)
            execute s:execution
            echo s:execution
        endif
    endfunction
    nnoremap <Plug>Plug_rpt_execute :call Plug_rpt_execute()<CR>
    nnoremap <leader>. :call Plug_rpt_execute()<CR>
" }}}
" }}}

" ç•Œé¢ç®¡ç† {{{
" theme {{{ Vimä¸»é¢˜(ColorScheme, StatusLine, TabLine)
    Plug 'morhetz/gruvbox'
    set rtp+=$DotVimPath/bundle/gruvbox
    let g:gruvbox_contrast_dark='soft'  " èƒŒæ™¯é€‰é¡¹ï¼šdark, medium, soft
    Plug 'junegunn/seoul256.vim'
    set rtp+=$DotVimPath/bundle/seoul256.vim
    let g:seoul256_background=236       " 233(æš—) ~ 239(äº®)
    let g:seoul256_light_background=256 " 252(æš—) ~ 256(äº®)
    Plug 'altercation/vim-colors-solarized'
    set rtp+=$DotVimPath/bundle/vim-colors-solarized
    Plug 'sainnhe/vim-color-forest-night'
    set rtp+=$DotVimPath/bundle/vim-color-forest-night
    Plug 'srcery-colors/srcery-vim'
    set rtp+=$DotVimPath/bundle/srcery-vim
if !s:gset.use_lightline
    try
        set background=dark
        colorscheme gruvbox
    " E185: æ‰¾ä¸åˆ°ä¸»é¢˜
    catch /^Vim\%((\a\+)\)\=:E185/
        silent! colorscheme desert
    endtry
else
    Plug 'yehuohan/lightline.vim'
    " î‚  î‚¡ î‚¢ î‚£ î‚° î‚± î‚² î‚³ î‚´ î‚µ î‚¶ î‚· î‚¸ î‚¹ î‚º î‚» î‚¼ î‚½ î‚¾ î‚¿
    " â–º âœ˜ â– âŒ˜ â–« â–ª â˜… â˜† â€¢ â‰¡ á¨ â™¥
    let g:lightline = {
        \ 'enable' : {'statusline': 1, 'tabline': 0},
        \ 'colorscheme' : 'gruvbox',
        \ 'active': {
                \ 'left' : [['mode'],
                \           ['all_filesign'],
                \           ['msg_left']],
                \ 'right': [['all_lineinfo', 'chk_indent', 'chk_trailing'],
                \           ['all_format'],
                \           ['msg_right']],
                \ },
        \ 'inactive': {
                \ 'left' : [['msg_left']],
                \ 'right': [['lite_info'],
                \           ['msg_right']],
                \ },
        \ 'tabline' : {
                \ 'left' : [['tabs']],
                \ 'right': [['close']],
                \ },
        \ 'component': {
                \ 'all_filesign': '%{winnr()},%-n%{&ro?",î‚¢":""}%M',
                \ 'all_format'  : '%{&ft!=#""?&ft."î‚½":""}%{&fenc!=#""?&fenc:&enc}î‚½%{&ff}',
                \ 'all_lineinfo': '0x%02B â‰¡%3p%% î‚¡ %04l/%L î‚£ %-2v',
                \ 'lite_info'   : '%p%%â‰¡%L',
                \ },
        \ 'component_function': {
                \ 'mode'        : 'Plug_ll_mode',
                \ 'msg_left'    : 'Plug_ll_msgLeft',
                \ 'msg_right'   : 'Plug_ll_msgRight',
                \ 'chk_indent'  : 'Plug_ll_checkMixedIndent',
                \ 'chk_trailing': 'Plug_ll_checkTrailing',
                \ },
        \ }
    if s:gset.use_powerfont
        let g:lightline.separator            = {'left': 'î‚°', 'right': 'î‚º'}
        let g:lightline.subseparator         = {'left': 'î‚±', 'right': 'î‚»'}
        let g:lightline.tabline_separator    = {'left': 'î‚°', 'right': 'î‚º'}
        let g:lightline.tabline_subseparator = {'left': 'î‚±', 'right': 'î‚»'}
    endif
    try
        set background=dark
        colorscheme gruvbox
    " E185: æ‰¾ä¸åˆ°ä¸»é¢˜
    catch /^Vim\%((\a\+)\)\=:E185/
        silent! colorscheme desert
        let g:lightline.colorscheme = 'solarized'
    endtry
    let g:lightline.blacklist = {'tagbar':0, 'nerdtree':0, 'Popc':0}
    let s:lightline_check_flg = 1       " æ˜¯å¦æ£€æµ‹Tabå’ŒTrailing
    nnoremap <leader>tl :call lightline#toggle()<CR>
    nnoremap <leader>tk :call Plug_ll_toggleCheck()<CR>

    " Augroup: PluginLightline {{{
    augroup PluginLightline
        autocmd!
        autocmd ColorScheme * call s:Plug_ll_colorScheme()
        autocmd BufReadPre * call s:Plug_ll_checkSize(getfsize(expand('<afile>')))
    augroup END
    function! s:Plug_ll_colorScheme()
        if !exists('g:loaded_lightline')
            return
        endif
        try
            let g:lightline.colorscheme = g:colors_name
            call lightline#init()
            call lightline#colorscheme()
            call lightline#update()
        " E117: å‡½æ•°ä¸å­˜åœ¨
        catch /^Vim\%((\a\+)\)\=:E117/
        endtry
    endfunction
    function! s:Plug_ll_checkSize(size)
        let s:lightline_check_flg = (a:size > 1024*1024*2 || a:size == -2) ? 0 : 1
    endfunction
    " }}}
    " FUNCTION: Plug_ll_toggleCheck() {{{
    function! Plug_ll_toggleCheck()
        let s:lightline_check_flg = !s:lightline_check_flg
        echo 's:lightline_check_flg = ' . s:lightline_check_flg
    endfunction
    " }}}
    " FUNCTION: Plug_ll_mode() {{{
    function! Plug_ll_mode()
        return &ft ==# 'tagbar' ? 'Tagbar' :
            \ &ft ==# 'nerdtree' ? 'NERDTree' :
            \ &ft ==# 'qf' ? (QuickfixGet()[0] ==# 'c' ? 'Quickfix' : 'Location') :
            \ &ft ==# 'help' ? 'Help' :
            \ &ft ==# 'Popc' ? popc#ui#GetStatusLineSegments('l')[0] :
            \ &ft ==# 'startify' ? 'Startify' :
            \ winwidth(0) > 60 ? lightline#mode() : ''
    endfunction
    " }}}
    " FUNCTION: Plug_ll_msgLeft() {{{
    function! Plug_ll_msgLeft()
        if &ft ==# 'qf'
            return 'CWD = ' . getcwd()
        else
            let l:fw = FindWowGetArgs()
            let l:fp = expand('%:p')
            return empty(l:fw) ? l:fp : substitute(l:fp, escape(l:fw[0], '\'), '', '')
        endif
    endfunction
    " }}}
    " FUNCTION: Plug_ll_msgRight() {{{
    function! Plug_ll_msgRight()
        let l:fw = FindWowGetArgs()
        return empty(l:fw) ? '' : (l:fw[0] . '[' . l:fw[1] . '(' . join(l:fw[2],',') . ')]')
    endfunction
    " }}}
    " FUNCTION: Plug_ll_checkMixedIndent() {{{
    function! Plug_ll_checkMixedIndent()
        if !s:lightline_check_flg
            return ''
        endif
        let l:ret = search('\t', 'nw')
        return (l:ret == 0) ? '' : 'I:'.string(l:ret)
    endfunction
    " }}}
    " FUNCTION: Plug_ll_checkTrailing() {{{
    function! Plug_ll_checkTrailing()
        if !s:lightline_check_flg
            return ''
        endif
        let ret = search('\s\+$', 'nw')
        return (l:ret == 0) ? '' : 'T:'.string(l:ret)
    endfunction
    " }}}
endif
" }}}

" rainbow {{{ å½©è‰²æ‹¬å·
    Plug 'luochen1990/rainbow'
    let g:rainbow_active = 1
    nnoremap <leader>tr :RainbowToggle<CR>
" }}}

" indentLine {{{ æ˜¾ç¤ºç¼©è¿›æ ‡è¯†
    Plug 'Yggdroot/indentLine'
    "let g:indentLine_char = '|'        " è®¾ç½®æ ‡è¯†ç¬¦æ ·å¼
    let g:indentLinet_color_term=200    " è®¾ç½®æ ‡è¯†ç¬¦é¢œè‰²
    nnoremap <leader>ti :IndentLinesToggle<CR>
" }}}

" popset {{{ å¼¹å‡ºé€‰é¡¹
    Plug 'yehuohan/popset'
    let g:Popset_SelectionData = [
        \{
            \ 'opt' : ['filetype', 'ft'],
            \ 'dsr' : 'When this option is set, the FileType autocommand event is triggered.',
            \ 'lst' : ['cpp', 'c', 'python', 'julia', 'vim', 'go', 'markdown', 'help', 'text',
                     \ 'sh', 'conf', 'make', 'cmake', 'javascript', 'json', 'html'],
            \ 'dic' : {
                    \ 'cpp'        : 'Cpp file',
                    \ 'c'          : 'C file',
                    \ 'python'     : 'Python script file',
                    \ 'julia'      : 'Julia script file',
                    \ 'vim'        : 'Vim script file',
                    \ 'go'         : 'Go Language',
                    \ 'markdown'   : 'MarkDown file',
                    \ 'help'       : 'Vim help doc',
                    \ 'text'       : 'Simple text',
                    \ 'sh'         : 'Linux shell script',
                    \ 'conf'       : 'Config file',
                    \ 'make'       : 'Makefile of .mak file',
                    \ 'cmake'      : 'CMakeLists.txt file',
                    \ 'javascript' : 'JavaScript file',
                    \ 'json'       : 'Json file',
                    \ 'html'       : 'Html file',
                    \},
            \ 'cmd' : 'popset#data#SetEqual',
        \},
        \{
            \ 'opt' : ['colorscheme', 'colo'],
            \ 'lst' : ['forest-night', 'gruvbox', 'seoul256', 'seoul256-light', 'solarized', 'srcery'],
            \ 'cmd' : '',
        \}
    \ ]
    " set option with PopSet
    nnoremap <leader><leader>s :call feedkeys(':PopSet ', 'n')<CR>
    nnoremap <leader>sa :PopSet popset<CR>
" }}}

" popc {{{ bufferç®¡ç†
    Plug 'yehuohan/popc'
    set hidden
    let g:Popc_jsonPath = $DotVimPath
    let g:Popc_useGlobalPath = 1
    let g:Popc_useTabline = 1
    let g:Popc_useStatusline = 1
    let g:Popc_usePowerFont = s:gset.use_powerfont
    let g:Popc_separator = {'left' : 'î‚°', 'right': 'î‚º'}
    let g:Popc_subSeparator = {'left' : 'î‚±', 'right': 'î‚»'}
    let s:popc_tabline_layout = 1
    nnoremap <leader><leader>h :PopcBuffer<CR>
    nnoremap <M-i> :PopcBufferSwitchLeft<CR>
    nnoremap <M-o> :PopcBufferSwitchRight<CR>
    nnoremap <leader><leader>b :PopcBookmark<CR>
    nnoremap <leader><leader>w :PopcWorkspace<CR>
    nnoremap <leader>wf :call Plug_popc_wksSearch()<CR>
    nnoremap <leader>ty :call Plug_popc_toggleLayout()<CR>
    function! Plug_popc_wksSearch()
        let l:wks_root = popc#layer#wks#GetCurrentWks()[1]
        if !empty(l:wks_root)
            execute ':LeaderfFile ' . l:wks_root
        endif
    endfunction
    function! Plug_popc_toggleLayout()
        if s:popc_tabline_layout == 0
            call popc#ui#TabLineSetLayout('buffer', 'tab')
            let s:popc_tabline_layout = 1
        elseif s:popc_tabline_layout == 1
            call popc#ui#TabLineSetLayout('buffer', '')
            let s:popc_tabline_layout = 2
        elseif s:popc_tabline_layout == 2
            call popc#ui#TabLineSetLayout('', 'tab')
            let s:popc_tabline_layout = 0
        endif
    endfunction
" }}}

" nerdtree {{{ ç›®å½•æ ‘å¯¼èˆª
    Plug 'scrooloose/nerdtree', {'on': ['NERDTreeToggle', 'NERDTree']}
    let g:NERDTreeShowHidden = 1
    let g:NERDTreeMapPreview = 'go'
    let g:NERDTreeMapOpenInTabSilent = 'gt'
    let g:NERDTreeMapOpenSplit = 's'
    let g:NERDTreeMapPreviewSplit = 'gs'
    let g:NERDTreeMapOpenVSplit = 'i'
    let g:NERDTreeMapPreviewVSplit = 'gi'
    let g:NERDTreeMapJumpNextSibling = '<C-n>'
    let g:NERDTreeMapJumpPrevSibling = '<C-p>'
    let g:NERDTreeMapChangeRoot = 'cd'  " æ›´æ”¹æ ¹ç›®å½•
    let g:NERDTreeMapChdir = 'CW'       " æ›´æ”¹CWD
    let g:NERDTreeMapCWD = 'CD'         " æ›´æ”¹æ ¹ç›®å½•ä¸ºCWD
    nnoremap <leader>te :NERDTreeToggle<CR>
    nnoremap <leader>tE :execute ':NERDTree ' . expand('%:p:h')<CR>
" }}}

" startify {{{ vimä¼šè¯ç•Œé¢
if s:gset.use_startify
    Plug 'mhinz/vim-startify'
    if IsLinux()
        let g:startify_bookmarks = [ {'c': '~/.vimrc'},
                                    \ '~/.zshrc',
                                    \ '~/.config/nvim/init.vim',
                                    \ '~/.config/i3/config'
                                    \]
    elseif IsWin()
        let g:startify_bookmarks = [ {'c': '$DotVimPath/../_vimrc'},
                                    \ '$DotVimPath/.ycm_extra_conf.py',
                                    \ '$LOCALAPPDATA/nvim/init.vim'
                                    \]
    elseif IsMac()
        let g:startify_bookmarks = [ {'c': '~/.vimrc'}, '~/.zshrc']
    endif
    let g:startify_lists = [
            \ {'type': 'bookmarks', 'header': ['   Bookmarks']},
            \ {'type': 'files',     'header': ['   Recent Files']},
            \ ]
    let g:startify_files_number = 7
    nnoremap <leader>su :Startify<CR>
endif
" }}}

" signature {{{ ä¹¦ç­¾ç®¡ç†
    Plug 'kshenoy/vim-signature'
    let g:SignatureMap = {
        \ 'Leader'             :  "m",
        \ 'PlaceNextMark'      :  "m,",
        \ 'ToggleMarkAtLine'   :  "m.",
        \ 'PurgeMarksAtLine'   :  "m-",
        \ 'DeleteMark'         :  "dm",
        \ 'PurgeMarks'         :  "m<Space>",
        \ 'PurgeMarkers'       :  "m<BS>",
        \ 'GotoNextLineAlpha'  :  "']",
        \ 'GotoPrevLineAlpha'  :  "'[",
        \ 'GotoNextSpotAlpha'  :  "`]",
        \ 'GotoPrevSpotAlpha'  :  "`[",
        \ 'GotoNextLineByPos'  :  "]'",
        \ 'GotoPrevLineByPos'  :  "['",
        \ 'GotoNextSpotByPos'  :  "]`",
        \ 'GotoPrevSpotByPos'  :  "[`",
        \ 'GotoNextMarker'     :  "]-",
        \ 'GotoPrevMarker'     :  "[-",
        \ 'GotoNextMarkerAny'  :  "]=",
        \ 'GotoPrevMarkerAny'  :  "[=",
        \ 'ListBufferMarks'    :  "m/",
        \ 'ListBufferMarkers'  :  "m?"
    \ }
    nnoremap <leader>ts :SignatureToggleSigns<CR>
    nnoremap <leader>ma :SignatureListBufferMarks<CR>
    nnoremap <leader>mc :<C-U>call signature#mark#Purge('all')<CR>
    nnoremap <leader>mx :<C-U>call signature#marker#Purge()<CR>
    nnoremap <M-,> :<C-U>call signature#mark#Goto('prev', 'line', 'pos')<CR>
    nnoremap <M-.> :<C-U>call signature#mark#Goto('next', 'line', 'pos')<CR>
" }}}

" undotree {{{ æ’¤æ¶ˆå†å²
    Plug 'mbbill/undotree'
    nnoremap <leader>tu :UndotreeToggle<CR>
" }}}
" }}}

" ä»£ç ç¼–å†™ {{{
" YouCompleteMe {{{ è‡ªåŠ¨è¡¥å…¨
if s:gset.use_ycm
    " FUNCTION: Plug_ycm_build(info) {{{
    " Linux: éœ€è¦python-dev, python3-dev, cmake, llvm, clang
    " Windows: éœ€è¦python, cmake, VS, 7-zip
    " Params: install.pyå®‰è£…å‚æ•°
    "   --clang-completer : C-famlilyï¼ŒåŸºäºClangè¡¥å…¨
    "   --go-completer    : Goï¼ŒåŸºäºGocode/Godefè¡¥å…¨ï¼Œéœ€è¦å®‰è£…Go
    "   --ts-completer    : Javascriptå’ŒTypeScriptï¼ŒåŸºäºTSServerè¡¥å…¨ï¼Œéœ€è¦å®‰è£…nodeå’Œnpm
    "   --java-completer  : Javaè¡¥å…¨ï¼Œéœ€è¦å®‰è£…JDK8
    function! Plug_ycm_build(info)
        " info is a dictionary with 3 fields
        " - name:   name of the plugin
        " - status: 'installed', 'updated', or 'unchanged'
        " - force:  set on PlugInstall! or PlugUpdate!
        if a:info.status == 'installed' || a:info.force
            if IsLinux()
                !python install.py --clang-completer --go-completer --java-completer --build-dir ycm_build
            elseif IsWin()
                !python install.py --clang-completer --go-completer --java-completer --ts-completer --msvc 15 --build-dir ycm_build
            endif
        endif
    endfunction
    " }}}
    Plug 'ycm-core/YouCompleteMe', {'do': function('Plug_ycm_build'), 'on': []}
    call s:plug.append('YouCompleteMe')
    let g:ycm_global_ycm_extra_conf=$DotVimPath.'/.ycm_extra_conf.py'
                                                                " C-familyè¡¥å…¨è·¯å¾„
    let g:ycm_enable_diagnostic_signs = 1                       " å¼€å¯è¯­æ³•æ£€æµ‹
    let g:ycm_max_diagnostics_to_display = 30
    let g:ycm_warning_symbol = 'â–º'                              " Warningç¬¦å·
    let g:ycm_error_symbol = 'âœ˜'                                " Errorç¬¦å·
    let g:ycm_auto_start_csharp_server = 0                      " ç¦æ­¢C#è¡¥å…¨
    let g:ycm_cache_omnifunc = 0                                " ç¦æ­¢ç¼“å­˜åŒ¹é…é¡¹ï¼Œæ¯æ¬¡éƒ½é‡æ–°ç”ŸæˆåŒ¹é…é¡¹
    let g:ycm_complete_in_strings = 1                           " å¼€å¯å¯¹å­—ç¬¦ä¸²è¡¥å…¨
    let g:ycm_complete_in_comments = 1                          " å¼€å¯å¯¹æ³¨é‡Šè¡¥å…¨
    let g:ycm_collect_identifiers_from_comments_and_strings = 0 " æ”¶é›†æ³¨é‡Šå’Œå­—ç¬¦ä¸²è¡¥å…¨
    let g:ycm_collect_identifiers_from_tags_files = 1           " æ”¶é›†æ ‡ç­¾è¡¥å…¨
    let g:ycm_seed_identifiers_with_syntax = 1                  " æ”¶é›†è¯­æ³•å…³é”®å­—è¡¥å…¨
    let g:ycm_use_ultisnips_completer = 1                       " æ”¶é›†UltiSnipsè¡¥å…¨
    let g:ycm_autoclose_preview_window_after_insertion = 1      " è‡ªåŠ¨å…³é—­é¢„è§ˆçª—å£
    let g:ycm_filetype_blacklist = {
        \ 'tagbar': 1,
        \ 'notes': 1,
        \ 'netrw': 1,
        \ 'unite': 1,
        \ 'text': 1,
        \ 'vimwiki': 1,
        \ 'pandoc': 1,
        \ 'infolog': 1,
        \ 'mail': 1,
        \ }                                                     " ç¦ç”¨YCMçš„åˆ—è¡¨
    let g:ycm_filetype_whitelist = {'*': 1}                     " YCMåªåœ¨ç™½åå•å‡ºç°ä¸”é»‘åå•æœªå‡ºç°çš„filetypeå·¥ä½œ
    let g:ycm_language_server = [
        \ {
            \ 'name': 'dart',
            \ 'cmdline': ['dart',
            \       IsWin() ? 'C:/MyApps/dart-sdk/bin/snapshots/analysis_server.dart.snapshot' :
            \                 '/opt/dart-sdk/bin/snapshots/analysis_server.dart.snapshot',
            \       '--lsp'],
            \ 'filetypes': ['dart']
        \ },
        \ {
            \ 'name': 'julia',
            \ 'cmdline': ['julia', '--startup-file=no', '--history-file=no', '-e', '
            \       using LanguageServer;
            \       using Pkg;
            \       import StaticLint;
            \       import SymbolServer;
            \       env_path = dirname(Pkg.Types.Context().env.project_file);
            \       debug = false;
            \       server = LanguageServer.LanguageServerInstance(stdin, stdout, debug, env_path, "", Dict());
            \       server.runlinter = true;
            \       run(server);
            \       '],
            \ 'filetypes': ['julia']
        \ },
        \ ]                                                     " LSPæ”¯æŒ
    let g:ycm_key_detailed_diagnostics = ''                     " ç›´æ¥ä½¿ç”¨:YcmShowDetailedDiagnosticå‘½ä»¤
    let g:ycm_key_list_select_completion = ['<C-j>', '<C-n>', '<Down>']
    let g:ycm_key_list_previous_completion = ['<C-k>', '<C-p>', '<Up>']
    let g:ycm_key_list_stop_completion = ['<C-y>']              " å…³é—­è¡¥å…¨menu
    let g:ycm_key_invoke_completion = '<C-l>'                   " æ˜¾ç¤ºè¡¥å…¨å†…å®¹ï¼ŒYCMä½¿ç”¨completefuncï¼ˆC-X C-Uï¼‰
                                                                " YCMä¸æ”¯æŒçš„è¡¥å…¨ï¼Œé€šè¿‡omnifunc(C-X C-O)é›†æˆåˆ°YCMä¸Š
    imap <C-Space> <C-l>
    nnoremap <leader>gg :YcmCompleter<CR>
    nnoremap <leader>gt :YcmCompleter GoTo<CR>
    nnoremap <leader>gI :YcmCompleter GoToInclude<CR>
    nnoremap <leader>gd :YcmCompleter GoToDefinition<CR>
    nnoremap <leader>gD :YcmCompleter GoToDeclaration<CR>
    nnoremap <leader>gi :YcmCompleter GoToImplementation<CR>
    nnoremap <leader>gr :YcmCompleter GoToReferences<CR>
    nnoremap <leader>gp :YcmCompleter GetParent<CR>
    nnoremap <leader>gk :YcmCompleter GetDoc<CR>
    nnoremap <leader>gy :YcmCompleter GetType<CR>
    nnoremap <leader>gf :YcmCompleter FixIt<CR>
    nnoremap <leader>gc :YcmCompleter ClearCompilationFlagCache<CR>
    nnoremap <leader>gs :YcmCompleter RestartServer<CR>
    nnoremap <leader>yr :YcmRestartServer<CR>
    nnoremap <leader>yd :YcmShowDetailedDiagnostic<CR>
    nnoremap <leader>yD :YcmDiags<CR>
    nnoremap <leader>yc :call Plug_ycm_createConf('.ycm_extra_conf.py')<CR>
    nnoremap <leader>yj :call Plug_ycm_createConf('jsconfig.json')<CR>
    function! Plug_ycm_createConf(filename)
        " åœ¨å½“å‰ç›®å½•ä¸‹åˆ›å»ºé…ç½®æ–‡ä»¶
        if !filereadable(a:filename)
            let l:file = readfile($DotVimPath . '/' . a:filename)
            call writefile(l:file, a:filename)
        endif
        execute 'edit ' . a:filename
    endfunction
endif
" }}}

" LanguageClient-neovim {{{ ä»£ç è¡¥å…¨ï¼ˆä½¿ç”¨LSPï¼‰
if s:gset.use_lcn
    Plug 'autozimu/LanguageClient-neovim', {
        \ 'branch': 'next',
        \ 'do': IsWin() ? 'powershell -executionpolicy bypass -File install.ps1' : 'bash install.sh',
        \ 'for': 'dart'
        \ }
    " LCNä½¿ç”¨omnifunc(LanguageClient#complete)
    let g:LanguageClient_serverCommands = {}
    let g:LanguageClient_diagnosticsDisplay = {}                " ç¦ç”¨è¯­æ³•æ£€æµ‹
    let g:LanguageClient_diagnosticsSignsMax = 0
    let g:LanguageClient_diagnosticsEnable = 0
    let g:LanguageClient_hasSnippetSupport = 0                  " ç¦ç”¨snippetæ”¯æŒ
endif
" }}}

" echodoc {{{ å‚æ•°æ–‡æ¡£æ˜¾ç¤º
    Plug 'Shougo/echodoc.vim'
    let g:echodoc_enable_at_startup = 1
if IsVim()
    let g:echodoc#type = 'echo'
else
    let g:echodoc#type = 'floating'
endif
    nnoremap <leader>td :call Plug_ed_toggle()<CR>
    function! Plug_ed_toggle()
        if echodoc#is_enabled()
            call echodoc#disable()
        else
            call echodoc#enable()
        endif
        echo 'Echo doc: ' . string(echodoc#is_enabled())
    endfunction
" }}}

" ultisnips {{{ ä»£ç ç‰‡æ®µ
if s:gset.use_ultisnips
    Plug 'yehuohan/ultisnips'           " snippetå¼•æ“ï¼ˆvmapçš„æ˜ å°„ï¼Œä¸vim-textmanipçš„<C-i>æœ‰å†²çªï¼‰
    Plug 'honza/vim-snippets'           " snippetåˆé›†
    " ä½¿ç”¨:UltiSnipsEditç¼–è¾‘g:UltiSnipsSnippetsDirä¸­çš„snippetæ–‡ä»¶
    let g:UltiSnipsSnippetsDir = $DotVimPath . '/vSnippets'
    let g:UltiSnipsSnippetDirectories=['UltiSnips', 'vSnippets']
                                        " è‡ªå®šä¹‰mySnippetsåˆé›†
    let g:UltiSnipsExpandTrigger='<Tab>'
    let g:UltiSnipsListSnippets='<C-Tab>'
    let g:UltiSnipsJumpForwardTrigger='<C-j>'
    let g:UltiSnipsJumpBackwardTrigger='<C-k>'
endif
" }}}

" ale {{{ è¯­æ³•æ£€æµ‹
    Plug 'dense-analysis/ale', {'on': 'ALEToggle'}
    " è¯­æ³•å¼•æ“:
    "   VimScript : vint
    let g:ale_completion_enabled = 0    " ä½¿èƒ½aleè¡¥å…¨(åªæ”¯æŒTypeScript)
    let g:ale_linters = {'java' : []}   " ç¦ç”¨Javaæ£€æµ‹ï¼ˆä¸YCMå†²çªï¼‰
    let g:ale_sign_error = 'âœ˜'
    let g:ale_sign_warning = 'â–º'
    let g:ale_set_loclist = 1
    let g:ale_set_quickfix = 0
    let g:ale_echo_delay = 10           " æ˜¾ç¤ºè¯­æ–‡é”™è¯¯çš„å»¶æ—¶æ—¶é—´
    let g:ale_lint_delay = 300          " æ–‡æœ¬æ›´æ”¹åçš„å»¶æ—¶æ£€æµ‹æ—¶é—´
    let g:ale_enabled = 0               " é»˜è®¤å…³é—­ALEæ£€æµ‹
    nnoremap <leader>ta :execute ':ALEToggle'<Bar>echo 'AleToggle: ' . g:ale_enabled<CR>
" }}}

" neoformat {{{ ä»£ç æ ¼å¼åŒ–
    Plug 'sbdchd/neoformat', {'on': 'Neoformat'}
    let g:neoformat_basic_format_align = 1
    let g:neoformat_basic_format_retab = 1
    let g:neoformat_basic_format_trim = 1
    let g:neoformat_c_astyle = {
        \ 'exe' : 'astyle',
        \ 'args' : ['--style=allman', '--pad-oper'],
        \ 'stdin' : 1,
        \ }
    let g:neoformat_cpp_astyle = g:neoformat_c_astyle
    let g:neoformat_java_astyle = {
        \ 'exe' : 'astyle',
        \ 'args' : ['--mode=java --style=google', '--pad-oper'],
        \ 'stdin' : 1,
        \ }
    let g:neoformat_python_autopep8 = {
        \ 'exe': 'autopep8',
        \ 'args': ['-s 4', '-E'],
        \ 'replace': 1,
        \ 'stdin': 1,
        \ 'env': ['DEBUG=1'],
        \ 'valid_exit_codes': [0, 23],
        \ 'no_append': 1,
        \ }
    let g:neoformat_enabled_c = ['astyle']
    let g:neoformat_enabled_cpp = ['astyle']
    let g:neoformat_enabled_java = ['astyle']
    let g:neoformat_enabled_python = ['autopep8']
    nnoremap <leader>fc :Neoformat<CR>
    vnoremap <leader>fc :Neoformat<CR>
" }}}

" surround {{{ æ·»åŠ åŒ…å›´ç¬¦
    Plug 'tpope/vim-surround'
    let g:surround_no_mappings = 1      " å–æ¶ˆé»˜è®¤æ˜ å°„
    " ä¿®æ”¹å’Œåˆ é™¤Surround
    nmap <leader>sd <Plug>Dsurround
    nmap <leader>sc <Plug>Csurround
    nmap <leader>sC <Plug>CSurround
    " ç»™Text Objectæ·»åŠ Surround
    nmap ys <Plug>Ysurround
    nmap yS <Plug>YSurround
    nmap <leader>sw ysiw
    nmap <leader>si ysw
    nmap <leader>sW ySiw
    nmap <leader>sI ySw
    " ç»™è¡Œæ·»åŠ Surround
    nmap <leader>sl <Plug>Yssurround
    nmap <leader>sL <Plug>YSsurround
    xmap <leader>sw <Plug>VSurround
    xmap <leader>sW <Plug>VgSurround
" }}}

" auto-pairs {{{ è‡ªåŠ¨æ‹¬å·
    Plug 'jiangmiao/auto-pairs'
    let g:AutoPairsShortcutToggle=''
    let g:AutoPairsShortcutFastWrap=''
    let g:AutoPairsShortcutJump=''
    let g:AutoPairsShortcutFastBackInsert=''
    nnoremap <leader>tp :call AutoPairsToggle()<CR>
"}}}

" tagbar {{{ ä»£ç ç»“æ„æŸ¥çœ‹
    Plug 'majutsushi/tagbar'
    let g:tagbar_width=30
    let g:tagbar_map_showproto=''       " å–æ¶ˆtagbarå¯¹<Space>çš„å ç”¨
    nnoremap <leader>tt :TagbarToggle<CR>
                                        " å¯ä»¥ ctags -R å‘½ä»¤è‡ªè¡Œç”Ÿæˆtags
" }}}

" nerdcommenter {{{ æ‰¹é‡æ³¨é‡Š
    Plug 'scrooloose/nerdcommenter'
    let g:NERDCreateDefaultMappings = 0
    let g:NERDSpaceDelims = 0           " åœ¨Commentåæ·»åŠ Space
    nmap <leader>cc <Plug>NERDCommenterComment
    nmap <leader>cm <Plug>NERDCommenterMinimal
    nmap <leader>cs <Plug>NERDCommenterSexy
    nmap <leader>cb <Plug>NERDCommenterAlignBoth
    nmap <leader>cl <Plug>NERDCommenterAlignLeft
    nmap <leader>ci <Plug>NERDCommenterInvert
    nmap <leader>cy <Plug>NERDCommenterYank
    nmap <leader>ce <Plug>NERDCommenterToEOL
    nmap <leader>ca <Plug>NERDCommenterAppend
    nmap <leader>ct <Plug>NERDCommenterAltDelims
    nmap <leader>cu <Plug>NERDCommenterUncomment
" }}}

" fswitch {{{ c/c++æ–‡ä»¶åˆ‡æ¢
    Plug 'derekwyatt/vim-fswitch', {'for': ['cpp', 'c']}
    nnoremap <Leader>of :FSHere<CR>
    nnoremap <Leader>os :FSSplitRight<CR>
    let g:fsnonewfiles='on'
" }}}

" asyncrun {{{ å¯¼æ­¥è¿è¡Œç¨‹åº
    Plug 'skywind3000/asyncrun.vim'
    if IsWin()
        let g:asyncrun_encs = 'cp936'   " å³'gbk'ç¼–ç 
    endif
    let g:asyncrun_open = 8             " è‡ªåŠ¨æ‰“å¼€quickfix window
    let g:asyncrun_save = 1             " è‡ªåŠ¨ä¿å­˜å½“å‰æ–‡ä»¶
    let g:asyncrun_local = 1            " ä½¿ç”¨setlocalçš„efm
    nnoremap <leader><leader>r :call feedkeys(':AsyncRun ', 'n')<CR>
    vnoremap <leader><leader>r :AsyncRun
    nnoremap <leader>rr :call feedkeys(':AsyncRun ', 'n')<CR>
    vnoremap <leader>rr :AsyncRun
    nnoremap <leader>rs :AsyncStop<CR>
" }}}

" easydebugger {{{ NodeJS, Go, Pythonè°ƒè¯•å™¨(Vim only)
if IsVim()
    Plug 'jayli/vim-easydebugger', {'for': ['python', 'go']}
    " å¯åŠ¨å…³é—­NodeJS/Python/Goè°ƒè¯•
    nmap <F5>   <Plug>EasyDebuggerInspect
    nmap <S-F5> <Plug>EasyDebuggerExit
    " æš‚åœç»§ç»­
    nmap <F6>   <Plug>EasyDebuggerContinue
    tmap <F6>   <Plug>EasyDebuggerContinue
    nmap <S-F6>   <Plug>EasyDebuggerPause
    tmap <S-F6>   <Plug>EasyDebuggerPause
    " è®¾ç½®æ–­ç‚¹
    nmap <F9>   <Plug>EasyDebuggerSetBreakPoint
    " å•æ­¥ï¼šStep over, into, out
    nmap <F10>  <Plug>EasyDebuggerNext
    tmap <F10>  <Plug>EasyDebuggerNext
    nmap <F11>  <Plug>EasyDebuggerStepIn
    tmap <F11>  <Plug>EasyDebuggerStepIn
    nmap <S-F11>    <Plug>EasyDebuggerStepOut
    tmap <S-F11>    <Plug>EasyDebuggerStepOut
endif
" }}}

" quickhl {{{ å•è¯é«˜äº®
    Plug 't9md/vim-quickhl'
    nmap <leader>hw <Plug>(quickhl-manual-this)
    xmap <leader>hw <Plug>(quickhl-manual-this)
    nmap <leader>hs <Plug>(quickhl-manual-this-whole-word)
    xmap <leader>hs <Plug>(quickhl-manual-this-whole-word)
    nmap <leader>hc <Plug>(quickhl-manual-clear)
    xmap <leader>hc <Plug>(quickhl-manual-clear)
    nmap <leader>hr <Plug>(quickhl-manual-reset)
    nmap <leader>th <Plug>(quickhl-manual-toggle)
" }}}

" illuminate {{{ è‡ªåŠ¨é«˜äº®
    Plug 'RRethy/vim-illuminate'
    let g:Illuminate_delay = 250
    let g:Illuminate_ftblacklist = ['nerdtree', 'tagbar']
    highlight link illuminatedWord MatchParen
    nnoremap <leader>tg :IlluminationToggle<CR>
" }}}

" colorizer {{{ é¢œè‰²é¢„è§ˆ
    Plug 'lilydjwg/colorizer', {'on': 'ColorToggle'}
    let g:colorizer_nomap = 1
    let g:colorizer_startup = 0
    nnoremap <leader>tc :ColorToggle<CR>
" }}}

" FastFold {{{ æ›´æ–°æŠ˜å 
    Plug 'Konfekt/FastFold'
    nmap <leader>zu <Plug>(FastFoldUpdate)
    let g:fastfold_savehook = 0         " åªå…è®¸æ‰‹åŠ¨æ›´æ–°folds
    "let g:fastfold_fold_command_suffixes =  ['x','X','a','A','o','O','c','C']
    "let g:fastfold_fold_movement_commands = [']z', '[z', 'zj', 'zk']
                                        " å…è®¸æŒ‡å®šçš„å‘½ä»¤æ›´æ–°folds
" }}}

" cpp-enhanced-highlight {{{ c++è¯­æ³•é«˜äº®
    Plug 'octol/vim-cpp-enhanced-highlight', {'for': 'cpp'}
" }}}

" julia {{{ Juliaæ”¯æŒ
    Plug 'JuliaEditorSupport/julia-vim', {'for': 'julia'}
    let g:default_julia_version = 'devel'
    let g:latex_to_unicode_tab = 1      " ä½¿ç”¨<Tab>è¾“å…¥unicodeå­—ç¬¦
    nnoremap <leader>tn :call LaTeXtoUnicode#Toggle()<CR>
" }}}

" dart-plugin {{{ dartè¯­æ³•é«˜äº®
    Plug 'dart-lang/dart-vim-plugin', {'for': 'dart'}
" }}}
" }}}

" è½¯ä»¶å·¥å…· {{{
if s:gset.use_utils
" vimcdoc {{{ ä¸­æ–‡å¸®åŠ©æ–‡æ¡£
    Plug 'yianwillis/vimcdoc', {'for': 'help'}
" }}}

" MarkDown {{{
    Plug 'gabrielelana/vim-markdown', {'for': 'markdown'}
    let g:markdown_include_jekyll_support = 0
    let g:markdown_enable_mappings = 0
    let g:markdown_enable_spell_checking = 0
    let g:markdown_enable_folding = 1   " æ„Ÿè§‰MarkDownæŠ˜å å¼•èµ·å¡é¡¿æ—¶ï¼Œå…³é—­æ­¤é¡¹
    let g:markdown_enable_conceal = 1   " åœ¨Vimä¸­æ˜¾ç¤ºMarkDowné¢„è§ˆ

    Plug 'iamcco/markdown-preview.nvim', {'for': 'markdown', 'do': { -> mkdp#util#install()}}
    let g:mkdp_auto_start = 0
    let g:mkdp_auto_close = 1
    let g:mkdp_refresh_slow = 0         " å³æ—¶é¢„è§ˆMarkDown
    let g:mkdp_command_for_global = 0   " åªæœ‰markdownæ–‡ä»¶å¯ä»¥é¢„è§ˆ
    let g:mkdp_browser = 'firefox'
    nnoremap <leader>vm :call Plug_md_view()<CR>
    nnoremap <leader>tb :call Plug_md_toggleBrowser()<CR>
    function! Plug_md_view() abort
        if !get(b:, 'MarkdownPreviewToggleBool')
            echo 'Open markdown preview'
        else
            echo 'Close markdown preview'
        endif
        call mkdp#util#toggle_preview()
    endfunction
    function! Plug_md_toggleBrowser()
        if g:mkdp_browser ==# 'firefox'
            let g:mkdp_browser = 'chrome'
        else
            let g:mkdp_browser = 'firefox'
        endif
        let g:mkdp_browser = g:mkdp_browser
        echo 'Browser toggle to ' . g:mkdp_browser
    endfunction
" }}}

" reStructruedText {{{
if !(IsWin() && IsNVim())
    " éœ€è¦å®‰è£… https://github.com/Rykka/instant-rst.py
    Plug 'Rykka/riv.vim', {'for': 'rst'}
    Plug 'Rykka/InstantRst', {'for': 'rst'}
    let g:instant_rst_browser = 'firefox'
if IsWin()
    " éœ€è¦å®‰è£… https://github.com/mgedmin/restview
    nnoremap <leader>vr :execute ':AsyncRun restview ' . expand('%:p:t')<Bar>cclose<CR>
else
    nnoremap <leader>vr :call Plug_rst_view()<CR>
    function! Plug_rst_view() abort
        if g:_instant_rst_daemon_started
            StopInstantRst
            echo 'StopInstantRst'
        else
            InstantRst
        endif
    endfunction
endif
endif
" }}}

" vimtex {{{ Latexæ”¯æŒ
    Plug 'lervag/vimtex', {'for': 'tex'}
    let g:vimtex_view_general_viewer = 'SumatraPDF'
    let g:vimtex_complete_enabled = 1   " ä½¿ç”¨vimtex#complete#omnifuncè¡¥å…¨
    let g:vimtex_complete_close_braces = 1
    let g:vimtex_compiler_method = 'latexmk'
                                        " TexLiveä¸­åŒ…å«äº†latexmk
    nmap <leader>at <Plug>(vimtex-toc-toggle)
    nmap <leader>al <Plug>(vimtex-compile)
    nmap <leader>aL <Plug>(vimtex-compile-ss)
    nmap <leader>ac <Plug>(vimtex-clean)
    nmap <leader>as <Plug>(vimtex-stop)
    nmap <leader>av <Plug>(vimtex-view)
    nmap <leader>am <Plug>(vimtex-toggle-main)
    " æ·»åŠ YCMé›†æˆ
    augroup PluginVimtex
        autocmd!
        autocmd VimEnter * call s:Plug_vt_setYcmTrigger()
    augroup END
    function! s:Plug_vt_setYcmTrigger()
        try
            let g:ycm_semantic_triggers.tex=g:vimtex#re#youcompleteme
        " E121:å˜é‡ä¸å­˜åœ¨
        catch /^Vim\%((\a\+)\)\=:E121/
        endtry
    endfunction
" }}}

" open-browser.vim {{{ åœ¨çº¿æœç´¢
    Plug 'tyru/open-browser.vim'
    let g:openbrowser_default_search='baidu'
    nmap <leader>bs <Plug>(openbrowser-smart-search)
    vmap <leader>bs <Plug>(openbrowser-smart-search)
    " search funtion - google, baidu, github
    function! Plug_brw_search(engine, mode)
        if a:mode ==# 'n'
            call openbrowser#search(expand('<cword>'), a:engine)
        elseif a:mode ==# 'v'
            call openbrowser#search(GetSelected(), a:engine)
        endif
    endfunction
    nnoremap <leader>big :call feedkeys(':OpenBrowserSearch -google ', 'n')<CR>
    nnoremap <leader>bg  :call Plug_brw_search('google', 'n')<CR>
    vnoremap <leader>bg  :call Plug_brw_search('google', 'v')<CR>
    nnoremap <leader>bib :call feedkeys(':OpenBrowserSearch -baidu ', 'n')<CR>
    nnoremap <leader>bb  :call Plug_brw_search('baidu', 'n')<CR>
    vnoremap <leader>bb  :call Plug_brw_search('baidu', 'v')<CR>
    nnoremap <leader>bih :call feedkeys(':OpenBrowserSearch -github ', 'n')<CR>
    nnoremap <leader>bh  :call Plug_brw_search('github', 'n')<CR>
    vnoremap <leader>bh  :call Plug_brw_search('github', 'v')<CR>
"}}}
endif
" }}}

call plug#end()
call s:plug.init()
" }}} End

" User Functions {{{
" funcs {{{
" FUNCTION: GetSelected() {{{ è·å–é€‰åŒºå†…å®¹
function! GetSelected()
    let l:reg_var = getreg('0', 1)
    let l:reg_mode = getregtype('0')
    normal! gv"0y
    let l:word = getreg('0')
    call setreg('0', l:reg_var, l:reg_mode)
    return l:word
endfunction
" }}}

" FUNCTION: GetMultiFilesCompletion(arglead, cmdline, cursorpos) {{{ å¤šæ–‡ä»¶è‡ªåŠ¨è¡¥å…¨
function! GetMultiFilesCompletion(arglead, cmdline, cursorpos)
    let l:complete = []
    let l:arglead_list = ['']
    let l:arglead_first = ''
    let l:arglead_glob = ''
    let l:files_list = []

    " process glob path-string
    if !empty(a:arglead)
        let l:arglead_list = split(a:arglead, ' ')
        let l:arglead_first = join(l:arglead_list[0:-2], ' ')
        let l:arglead_glob = l:arglead_list[-1]
    endif

    " glob non-hidden and hidden files(but no . and ..) with ignorecase
    set wildignorecase
    set wildignore+=.,..
    let l:files_list = split(glob(l:arglead_glob . "*") . "\n" . glob(l:arglead_glob . "\.[^.]*"), "\n")
    set wildignore-=.,..

    if len(l:arglead_list) == 1
        let l:complete = l:files_list
    else
        for item in l:files_list
            call add(l:complete, l:arglead_first . ' ' . item)
        endfor
    endif
    return l:complete
endfunction
" }}}

" FUNCTION: GetFileList(pat, sdir) {{{ è·å–æ–‡ä»¶åˆ—è¡¨
" @param pat: æ–‡ä»¶åŒ¹é…æ¨¡å¼ï¼Œå¦‚*.pro
" @param sdir: æŸ¥æ‰¾èµ·å§‹ç›®å½•ï¼Œé»˜è®¤ä»å½“å‰ç›®å½•å‘ä¸ŠæŸ¥æ‰¾åˆ°æ ¹ç›®å½•
" @return è¿”å›æ‰¾åˆ°çš„æ–‡ä»¶åˆ—è¡¨
function! GetFileList(pat, sdir)
    let l:dir      = empty(a:sdir) ? expand('%:p:h') : a:sdir
    let l:dir_last = ''
    let l:pfile    = ''

    while l:dir !=# l:dir_last
        let l:pfile = glob(l:dir . '/' . a:pat)
        if !empty(l:pfile)
            break
        endif

        let l:dir_last = l:dir
        let l:dir = fnamemodify(l:dir, ':p:h:h')
    endwhile

    return split(l:pfile, "\n")
endfunction
" }}}

" FUNCTION: GetFileContent(fp, pat, flg) {{{ è·å–æ–‡ä»¶ä¸­ç‰¹å®šçš„å†…å®¹
" @param fp: ç›®å½•æ–‡ä»¶
" @param pat: åŒ¹é…æ¨¡å¼ï¼Œå¿…é¡»ä½¿ç”¨ \(\) æ¥æå–å­—ç¬¦ä¸²
" @param flg: åŒ¹é…æ‰€æœ‰è¿˜æ˜¯ç¬¬ä¸€ä¸ª
" @return è¿”å›åŒ¹é…çš„å†…å®¹åˆ—è¡¨
function! GetFileContent(fp, pat, flg)
    let l:content = []
    for l:line in readfile(a:fp)
        let l:result = matchlist(l:line, a:pat)
        if !empty(l:result)
            if a:flg ==# 'all'
                if !empty(l:result[1])
                    call add(l:content, l:result[1])
                endif
            elseif a:flg ==# 'first'
                return empty(l:result[1]) ? [] : [result[1]]
            endif
        endif
    endfor
    return l:content
endfunction
" }}}

" FUNCTION: GetContentRange(pats, pate) {{{ è·å–ç‰¹å®šçš„å†…å®¹çš„èŒƒå›´
" @param pats: èµ·å§‹è¡ŒåŒ¹é…æ¨¡å¼ï¼Œstartä¸ºpatsæ‰€åœ¨è¡Œ
" @param pate: ç»“æŸè¡ŒåŒ¹é…æ¨¡å¼ï¼Œendä¸ºpateæ‰€åœ¨è¡Œ
" @return è¿”å›åˆ—è¡¨[start, end]
function! GetContentRange(pats, pate)
    let l:start = search(a:pats, 'bcnW')
    let l:end = search(a:pate, 'cnW')
    if l:start == 0
        let l:start = 1
    endif
    if l:end == 0
        let l:end = line('$')
    endif
    return [l:start, l:end]
endfunction
" }}}

" FUNCTION: GetArgs(str) {{{ è§£æå­—ç¬¦ä¸²å‚æ•°åˆ°åˆ—è¡¨ä¸­
" @param str: å‚æ•°å­—ç¬¦ä¸²ï¼Œå¦‚ '"Test", 10, g:a'
" @return è¿”å›å‚æ•°åˆ—è¡¨ï¼Œå¦‚ ["Test", 10, g:a]
function! GetArgs(str)
    let l:args = []
    function! s:parseArgs(...) closure
        let l:args = a:000
    endfunction
    execute 'call s:parseArgs(' . a:str . ')'
    return l:args
endfunction
" }}}

" FUNCTION: GetInput(prompt, [text, completion, workdir]) {{{ è¾“å…¥å­—ç¬¦ä¸²
" @param workdir: è®¾ç½®å·¥ä½œç›®å½•ï¼Œç”¨äºæ–‡ä»¶å’Œç›®å½•è¡¥å…¨
function! GetInput(prompt, ...)
    if a:0 == 0
        return input(a:prompt)
    elseif a:0 == 1
        return input(a:prompt, a:1)
    elseif a:0 == 2
        return input(a:prompt, a:1, a:2)
    elseif a:0 == 3
        execute 'lcd ' . a:3
        return input(a:prompt, a:1, a:2)
    endif
endfunction
" }}}

" FUNCTION: FuncExecInput(iargs, fn, [fargs...]) range {{{
" @param iargs: ç”¨äºGetInputçš„å‚æ•°åˆ—è¡¨
" @param fn: è¦è¿è¡Œçš„å‡½æ•°ï¼Œç¬¬ä¸€ä¸ªå‚æ•°å¿…é¡»ä¸ºGetInputçš„è¾“å…¥
" @param fargs: fnçš„é™„åŠ å‚æ•°
function! FuncExecInput(iargs, fn, ...) range
    let l:inpt = call('GetInput', a:iargs)
    if empty(l:inpt)
        return
    endif
    let l:fargs = [l:inpt]
    if a:0 > 0
        call extend(l:fargs, a:000)
    endif
    let l:range = (a:firstline == a:lastline) ? '' : (string(a:firstline) . ',' . string(a:lastline))
    let Fn = function(a:fn, l:fargs)
    execute l:range . 'call Fn()'
endfunction
" }}}

" FUNCTION: FuncEditTempFile(suffix, ntab) {{{ ç¼–è¾‘ä¸´æ—¶æ–‡ä»¶
" @param suffix: ä¸´æ—¶æ–‡ä»¶é™„åŠ åç¼€
" @param ntab: åœ¨æ–°tabä¸­æ‰“å¼€
function! FuncEditTempFile(suffix, ntab)
    let l:tempfile = fnamemodify(tempname(), ':r')
    if empty(a:suffix)
        let l:tempfile .= '.tmp'
    else
        let l:tempfile .= '.' . a:suffix
    endif
    if a:ntab
        execute 'tabedit ' . l:tempfile
    else
        execute 'edit ' . l:tempfile
    endif
endfunction
"}}}

" FUNCTION: FuncDiffFile(filename, mode) {{{ æ–‡ä»¶å¯¹æ¯”
function! FuncDiffFile(filename, mode)
    if a:mode ==# 's'
        execute 'diffsplit ' . a:filename
    elseif a:mode ==# 'v'
        execute 'vertical diffsplit ' . a:filename
    endif
endfunction
" }}}

" FUNCTION: FuncDivideSpace(string, pos) range {{{ æ·»åŠ åˆ†éš”ç¬¦
function! FuncDivideSpace(string, pos) range
    let l:chars = split(a:string)

    for k in range(a:firstline, a:lastline)
        let l:line = getline(k)
        let l:fie = ' '
        for ch in l:chars
            let l:pch = '\m\s*\M' . escape(ch, '\') . '\m\s*\C'
            if a:pos == 'h'
                let l:sch = l:fie . escape(ch, '&\')
            elseif a:pos == 'c'
                let l:sch = l:fie . escape(ch, '&\') . l:fie
            elseif a:pos == 'l'
                let l:sch = escape(ch, '&\') . l:fie
            elseif a:pos == 'd'
                let l:sch = escape(ch, '&\')
            endif
            let l:line = substitute(l:line, l:pch, l:sch, 'g')
        endfor
        call setline(k, l:line)
    endfor
    call Plug_rpt_setExecution('call FuncDivideSpace("' . a:string . '", "' . a:pos . '")')
endfunction
let FuncDivideSpaceH = function('FuncExecInput', [['Divide H Space(split with space): '], 'FuncDivideSpace', 'h'])
let FuncDivideSpaceC = function('FuncExecInput', [['Divide C Space(split with space): '], 'FuncDivideSpace', 'c'])
let FuncDivideSpaceL = function('FuncExecInput', [['Divide L Space(split with space): '], 'FuncDivideSpace', 'l'])
let FuncDivideSpaceD = function('FuncExecInput', [['Delete D Space(split with space): '], 'FuncDivideSpace', 'd'])
" }}}

" FUNCTION: FuncAppendCmd(str, flg) {{{ å°†å‘½ä»¤ç»“æœä½œä¸ºæ–‡æœ¬æ’å…¥
function! FuncAppendCmd(str, flg)
    if a:flg ==# 'call'
        let l:as = match(a:str, '(')
        let l:ae = -1   " match(a:str, ')') - 1
        let l:str = a:str[0 : l:as - 1]
        let l:args = GetArgs(a:str[l:as + 1 : l:ae - 1])
        let l:result = call(l:str, l:args)
        if type(l:result) != v:t_string
            let l:result = string(l:result)
        endif
    elseif a:flg ==# 'exec'
        let l:result = execute(a:str)
    endif
    call append(line('.'), split(l:result, "\n"))
endfunction
let FuncAppendExecResult = function('FuncExecInput', [['Input command = ', '', 'command'] , 'FuncAppendCmd', 'exec'])
let FuncAppendCallResult = function('FuncExecInput', [['Input function = ', '', 'function'], 'FuncAppendCmd', 'call'])
" }}}

" FUNCTION: MiscFcitx2en() MiscFcitx2zh() {{{ åˆ‡æ¢Fcitxè¾“å…¥æ³•
if IsLinux()
function! MiscFcitx2en()
    if 2 == system('fcitx-remote')
        let l:t = system('fcitx-remote -c')
    endif
endfunction
function! MiscFcitx2zh()
    if 1 == system('fcitx-remote')
        let l:t = system('fcitx-remote -o')
    endif
endfunction
endif
" }}}

" FUNCTION: MiscGotoKeyword(mode) {{{ æŸ¥æ‰¾Vimå…³é”®å­—
function! MiscGotoKeyword(mode)
    let l:exec = 'help '
    if a:mode ==# 'n'
        let l:word = expand('<cword>')
    elseif a:mode ==# 'v'
        let l:word = GetSelected()
    endif

    " æ·»åŠ å…³é”®å­—
    let l:exec .= l:word
    if IsNVim()
        " nvimç”¨è‡ªå·±çš„å¸®åŠ©æ–‡ä»¶ï¼Œåªæœ‰è‹±æ–‡çš„
        let l:exec .= '@en'
    endif

    execute l:exec
endfunction
" }}}

" FUNCTION: MiscHoldTopLine() {{{ å†»ç»“é¡¶è¡Œ
function! MiscHoldTopLine()
    let l:line = line('.')
    split %
    resize 1
    call cursor(l:line, 1)
    wincmd p
endfunction
" }}}
" }}}

" project {{{
" Required: 'skywind3000/asyncrun.vim'
"           'yehuohan/popset'

" s:cpl {{{
" @attribute type: æ–‡ä»¶ç±»å‹
" @attribute wdir, args, srcf, outf: ç”¨äºtypeçš„å‚æ•°
" @attribute cell: ç”¨äºtypeçš„cellç±»å‹
" @attribute efm: ç”¨äºtypeçš„errorformatç±»å‹
" @attribute pro: é¡¹ç›®ç±»å‹
" @attribute pat: åŒ¹é…æ¨¡å¼å­—ç¬¦ä¸²
" @attribute sel_arg: é¢„ç½®CompileFileå‚æ•°è¾“å…¥
" @attribute sel_exe: é¢„ç½®å‘½ä»¤
let s:cpl = {
    \ 'wdir' : '',
    \ 'args' : '',
    \ 'srcf' : '',
    \ 'outf' : '',
    \ 'type' : {
        \ 'c'          : [IsWin() ? 'gcc %s -o %s %s && %s' : 'gcc %s -o %s %s && ./%s',
                                                               \ 'args' , 'outf' , 'srcf' , 'outf' ],
        \ 'cpp'        : [IsWin() ? 'g++ -std=c++11 %s -o %s %s && %s' : 'g++ -std=c++11 %s -o %s %s && ./%s',
                                                               \ 'args' , 'outf' , 'srcf' , 'outf' ],
        \ 'java'       : ['javac %s && java %s %s'             , 'srcf' , 'outf' , 'args'          ],
        \ 'python'     : ['python %s %s'                       , 'srcf' , 'args'                   ],
        \ 'julia'      : ['julia %s %s'                        , 'srcf' , 'args'                   ],
        \ 'lua'        : ['lua %s %s'                          , 'srcf' , 'args'                   ],
        \ 'go'         : ['go run %s %s'                       , 'srcf' , 'args'                   ],
        \ 'javascript' : ['node %s %s'                         , 'srcf' , 'args'                   ],
        \ 'dart'       : ['dart %s %s'                         , 'srcf' , 'args'                   ],
        \ 'tex'        : ['pdfLatex %s && SumatraPDF %s.pdf'   , 'srcf' , 'outf'                   ],
        \ 'sh'         : ['./%s %s'                            , 'srcf' , 'args'                   ],
        \ 'dosbatch'   : ['%s %s'                              , 'srcf' , 'args'                   ],
        \ 'markdown'   : ['typora %s'                          , 'srcf'                            ],
        \ 'json'       : ['python -m json.tool %s'             , 'srcf'                            ],
        \ 'matlab'     : ['matlab -nosplash -nodesktop -r %s'  , 'outf'                            ],
        \ 'html'       : ['firefox %s'                         , 'srcf'                            ],
        \ 'dot'        : ['dotty %s && dot -Tpng %s -o %s.png' , 'srcf' , 'srcf' , 'outf'          ],
        \ },
    \ 'cell' : {
        \ 'python' : ['python', '^#%%', '^#%%'],
        \ 'julia'  : ['julia', '^#%%', '^#%%'],
        \ 'lua'    : ['lua', '^--%%', '^--%%'],
        \ },
    \ 'efm' : {
        \ 'python' : '%*\\sFile\ \"%f\"\\,\ line\ %l\\,\ %m',
        \ },
    \ 'pro' : {
        \ 'qt'     : ['*.pro', 'CFnQt'],
        \ 'vs'     : ['*.sln', 'CFnVs'],
        \ 'mk'     : ['[mM]akefile', 'CFnMake'],
        \ 'sphinx' : [IsWin() ? 'make.bat' : '[mM]akefile', 'CFnSphinx'],
        \ },
    \ 'pat' : {
        \ 'target' : '\mTARGET\s*:\?=\s*\(\<[a-zA-Z_][a-zA-Z0-9_\-]*\)',
        \ },
    \ 'sel_arg' : {
        \ 'opt' : ['select args to CompileFile'],
        \ 'lst' : [
                \ '-g',
                \ '-finput-charset=utf-8 -fexec-charset=gbk',
                \ '-static',
                \ '-fPIC -shared'
                \ ],
        \ 'cmd' : {sopt, arg -> call('CompileFile', [arg])},
        \ },
    \ 'sel_exe' : {
        \ 'opt' : ['select execution to run'],
        \ 'lst' : [
                \ 'retab',
                \ '%s/\s\+$//g',
                \ '%s/\r//g',
                \ 'edit ++enc=utf-8',
                \ 'edit ++enc=cp936',
                \ 'AsyncRun python -m json.tool %',
                \ 'AsyncRun go mod init %:r',
                \ 'AsyncRun cflow -T %'
                \ ],
        \ 'dic' : {
                \ 'retab'            : 'retab with expandtab',
                \ '%s/\s\+$//g'      : 'remove trailing space',
                \ '%s/\r//g'         : 'remove ^M',
                \ 'edit ++enc=utf-8' : 'load as utf-8',
                \ 'edit ++enc=cp936' : 'load as cp936',
                \ },
        \ 'cmd' : {sopt, arg -> execute(arg)},
        \ }
    \}
" FUNCTION: s:cpl.printf(type, args, srcf, outf) dict {{{
" ç”Ÿæˆæ–‡ä»¶ç¼–è¯‘æˆ–æ‰§è¡Œå‘½ä»¤å­—ç¬¦ä¸²ã€‚
" @param type: ç¼–è¯‘ç±»å‹ï¼Œéœ€è¦åŒ…å«äºs:cpl.typeä¸­
" @param wdir: å‘½ä»¤è¿è¡Œç›®å½•
" @param args: å‚æ•°
" @param srcf: æºæ–‡ä»¶
" @param outf: ç›®æ ‡æ–‡ä»¶
" @return è¿”å›ç¼–è¯‘æˆ–æ‰§è¡Œå‘½ä»¤
function! s:cpl.printf(type, wdir, args, srcf, outf) dict
    if !has_key(s:cpl.type, a:type)
        \ || ('sh' ==? a:type && !(IsLinux() || IsGw() || IsMac()))
        \ || ('dosbatch' ==? a:type && !IsWin())
        throw 's:cpl.type doesn''t support "' . a:type . '"'
    endif
    let self.wdir = a:wdir
    let self.args = a:args
    let self.srcf = '"' . a:srcf . '"'
    let self.outf = '"' . a:outf . '"'
    let l:pstr = copy(self.type[a:type])
    call map(l:pstr, {key, val -> (key == 0) ? val : get(self, val, '')})
    " create exec string
    return self.run(a:type, self.wdir, call('printf', l:pstr))
endfunction
" }}}

" FUNCTION: s:cpl.run(type, wdir, cmd) dict {{{
" ç”Ÿæˆè¿è¡Œå‘½ä»¤å­—ç¬¦ä¸²ã€‚
" @param wdir: å‘½ä»¤è¿è¡Œç›®å½•
" @param type: errorformatç±»å‹ï¼Œåœ¨s:cpl.efmä¸­
" @param cmd: å‘½ä»¤å­—ç¬¦ä¸²
" @return è¿”å›è¿è¡Œå‘½ä»¤
function! s:cpl.run(type, wdir, cmd) dict
    if has_key(s:cpl.efm, a:type)
        execute 'setlocal efm=' . s:cpl.efm[a:type]
    endif
    let l:exec = ':AsyncRun '
    if !empty(a:wdir)
        let l:wdir = fnameescape(a:wdir)
        let l:exec .= '-cwd=' . l:wdir
        execute 'lcd ' . l:wdir
    endif
    return join([l:exec, a:cmd])
endfunction
" }}}

" FUNCTION: s:cpl.runcell(type) dict {{{
" @param type: cellç±»å‹ï¼ŒåŒæ—¶ä¹Ÿæ˜¯efmç±»å‹
function! s:cpl.runcell(type) dict
    if !has_key(s:cpl.cell, a:type)
        throw 's:cpl.cell doesn''t support "' . a:type . '"'
    endif
    if has_key(s:cpl.efm, a:type)
        execute 'setlocal efm=' . s:cpl.efm[a:type]
    endif
    let [l:bin, l:pats, l:pate] = s:cpl.cell[a:type]
    let l:range = GetContentRange(l:pats, l:pate)
    " create exec string
    return ':' . join(l:range, ',') . 'AsyncRun '. l:bin
endfunction
" }}}
" }}}

" FUNCTION: CompileFile(argstr) {{{
function! CompileFile(argstr)
    let l:type    = &filetype           " æ–‡ä»¶ç±»å‹
    let l:srcfile = expand('%:t')       " æ–‡ä»¶åï¼Œä¸å¸¦è·¯å¾„ï¼Œå¸¦æ‰©å±•å
    let l:outfile = expand('%:t:r')     " æ–‡ä»¶åï¼Œä¸å¸¦è·¯å¾„ï¼Œä¸å¸¦æ‰©å±•å
    let l:workdir = expand('%:p:h')     " å½“å‰æ–‡ä»¶ç›®å½•
    try
        let l:exec = s:cpl.printf(l:type, l:workdir, a:argstr, l:srcfile, l:outfile)
        execute l:exec
        call Plug_rpt_setExecution(l:exec)
    catch
        echo v:exception
    endtry
endfunction
" }}}

" FUNCTION: CompileCell(argstr) {{{
function! CompileCell()
    try
        let l:exec = s:cpl.runcell(&filetype)
        execute l:exec
        echo l:exec
        call Plug_rpt_setExecution(l:exec)
    catch
        echo v:exception
    endtry
endfunction
" }}}

" FUNCTION: CompileProject(type, args) {{{
" å½“æ‰¾åˆ°å¤šä¸ªProjectæ–‡ä»¶æ—¶ï¼Œä¼šå¼¹å‡ºé€‰é¡¹ä»¥ä¾›é€‰æ‹©ã€‚
" @param type: å·¥ç¨‹ç±»å‹ï¼Œç”¨äºè·å–å·¥ç¨‹è¿è¡Œå›è°ƒå‡½æ•°
"   é¡¹ç›®å›è°ƒå‡½æ•°éœ€è¦3ä¸ªå‚æ•°(å¯èƒ½ç”¨äºpopsetæ’ä»¶)ï¼š
"   - sopt: è‡ªå®šä¹‰å‚æ•°ä¿¡æ¯
"   - sel: Projectæ–‡ä»¶è·¯å¾„
"   - args: Projectçš„é™„åŠ å‚æ•°åˆ—è¡¨
" @param args: ç¼–è¯‘å·¥ç¨‹æ–‡ä»¶å‡½æ•°çš„é™„åŠ å‚æ•°ï¼Œéœ€è¦é‡‡ç”¨popsetæ’ä»¶
function! CompileProject(type, args)
    let [l:pat, l:fn] = s:cpl.pro[a:type]
    let l:prj = GetFileList(l:pat, '')
    if len(l:prj) == 1
        let Fn = function(l:fn)
        call Fn('', l:prj[0], a:args)
    elseif len(l:prj) > 1
        call PopSelection({
            \ 'opt' : ['Please select the project file'],
            \ 'lst' : l:prj,
            \ 'cmd' : a:fn,
            \ 'arg' : a:args
            \})
    else
        echo 'None of ' . l:pat . ' was found!'
    endif
endfunction
" }}}

" FUNCTION: CFnQt(sopt, sel, args) {{{
function! CFnQt(sopt, sel, args)
    let l:srcfile = fnamemodify(a:sel, ':p:t')
    let l:outfile = GetFileContent(a:sel, s:cpl.pat.target, 'first')
    let l:outfile = empty(l:outfile) ? fnamemodify(a:sel, ':t:r') : l:outfile[0]
    let l:workdir = fnamemodify(a:sel, ':p:h')

    if IsWin()
        let l:cmd = printf('cd "%s" && qmake -r "%s" && vcvars64.bat && nmake -f Makefile.Debug %s',
                    \ l:workdir, l:srcfile, join(a:args[1]))
    else
        let l:cmd = printf('cd "%s" && qmake "%s" && make %s'
                    \ l:workdir, l:srcfile, join(a:args[1]))
    endif
    if a:args[0]
        let l:cmd .= ' && "./' . l:outfile .'"'
    endif
    execute s:cpl.run('cpp', l:workdir, l:cmd)
endfunction
" }}}

" FUNCTION: CFnMake(sopt, sel, args) {{{
function! CFnMake(sopt, sel, args)
    let l:outfile = GetFileContent(a:sel, s:cpl.pat.target, 'first')
    let l:outfile = empty(l:outfile) ? '' : l:outfile[0]
    let l:workdir = fnamemodify(a:sel, ':p:h')

    let l:cmd = printf('cd "%s" && make %s', l:workdir, join(a:args[1]))
    if a:args[0]
        let l:cmd .= ' && "./' . l:outfile .'"'
    endif
    execute s:cpl.run('cpp', l:workdir, l:cmd)
endfunction
"}}}

" FUNCTION: CFnVs(sopt, sel, args) {{{
function! CFnVs(sopt, sel, args)
    let l:srcfile = fnamemodify(a:sel, ':p:t')
    let l:outfile = fnamemodify(a:sel, ':p:t:r')
    let l:workdir = fnamemodify(a:sel, ':p:h')

    let l:cmd = printf('cd "%s" && vcvars64.bat && devenv "%s" /%s',
                    \ l:workdir, l:srcfile, join(a:args[1]))
    if a:args[0]
        let l:cmd .= ' && "./' . l:outfile .'"'
    endif
    execute s:cpl.run('cpp', l:workdir, l:cmd)
endfunction
" }}}

" FUNCTION: CFnSphinx(sopt, sel, args) {{{
" @param args[0]: æ˜¯å¦ç›´æ¥æ‰“å¼€sphinxæ–‡æ¡£
function! CFnSphinx(sopt, sel, args)
    let l:outfile = 'build/html/index.html'
    let l:workdir = fnamemodify(a:sel, ':p:h')

    let l:cmd = printf('cd "%s" && make %s', l:workdir, a:args[1])
    if a:args[0]
        let l:cmd .= join([' && firefox', l:outfile])
    endif
    execute s:cpl.run('', l:workdir, l:cmd)
endfunction
"}}}

" Run compiler
let RcArg         = function('popset#set#PopSelection', [s:cpl.sel_arg])
let RcExe         = function('popset#set#PopSelection', [s:cpl.sel_exe])
let RcQt          = function('CompileProject', ['qt', [0, []]])
let RcQtRun       = function('CompileProject', ['qt', [1, []]])
let RcQtClean     = function('CompileProject', ['qt', [0, ['distclean']]])
let RcMake        = function('CompileProject', ['mk', [0, []]])
let RcMakeRun     = function('CompileProject', ['mk', [1, []]])
let RcMakeClean   = function('CompileProject', ['mk', [0, ['clean']]])
let RcVs          = function('CompileProject', ['vs', [0, ['Build']]])
let RcVsRun       = function('CompileProject', ['vs', [1, ['Build']]])
let RcVsClean     = function('CompileProject', ['vs', [0, ['Clean']]])
let RcSphinx      = function('CompileProject', ['sphinx', [0, 'html']])
let RcSphinxRun   = function('CompileProject', ['sphinx', [1, 'html']])
let RcSphinxClean = function('CompileProject', ['sphinx', [0, 'clean']])
" }}}

" find&search {{{
" Required: 'skywind3000/asyncrun.vim' or 'yegappan/grep' or 'mhinz/vim-grepper'
"           'Yggdroot/LeaderF', 'junegunn/fzf.vim'
"           'yehuohan/popc'
"           'yehuohan/popset'
augroup UserFunctionSearch
    autocmd!
    autocmd User Grepper call FindWowHighlight(s:fw.pat)
augroup END

" s:fw {{{
" @attribute engine: æœç´¢ç¨‹åºï¼Œå‘½ä»¤æ ¼å¼ä¸ºï¼šprintf('cmd %s %s %s',<pat>,<loc>,<opt>)
"            sr : search
"            sa : search append
"            sk : search kill
"            sl : search lines with fuzzy
"            ff : fuzzy files
"            fh : fuzzy huge linestext
" @attribute args: æœç´¢å‚æ•°
" @attribute rg: é¢„ç½®çš„rgæœç´¢å‘½ä»¤ï¼Œç”¨äºæœç´¢æŒ‡å®šæ–‡æœ¬
" @attribute fuzzy: é¢„ç½®çš„æ¨¡ç³Šæœç´¢å‘½ä»¤ï¼Œç”¨äºæ–‡ä»¶å’Œæ–‡æœ¬ç­‰æ¨¡ç³Šæœç´¢
" @attribute misc: æœç´¢é«˜äº®ç­‰å‚æ•°
" @attribute mappings: æ˜ å°„æŒ‰é”®
let s:fw = {
    \ 'cmd' : '',
    \ 'pat' : '',
    \ 'loc' : '',
    \ 'opt' : '',
    \ 'engine' : {
        \ 'sr' : '',
        \ 'sa' : '',
        \ 'sk' : '',
        \ 'sl' : ':Leaderf rg --nowrap -e "%s" "%s" %s',
        \ 'ff' : '',
        \ 'fh' : ''
    \ },
    \ 'args' : {
        \ 'root'    : '',
        \ 'filters' : '',
        \ 'globlst' : []
        \ },
    \ 'rg' : {
        \ 'asyncrun' : {
            \ 'sr' : ':botright copen | :AsyncRun! rg --vimgrep "%s" "%s" %s',
            \ 'sa' : ':botright copen | :AsyncRun! -append rg --vimgrep "%s" "%s" %s',
            \ 'sk' : ':AsyncStop'},
        \ 'grep' : {
            \ 'sr' : ':execute '':Rg '' . escape(''%s'', " ") . '' "%s" %s''',
            \ 'sa' : ':execute '':RgAdd '' . escape(''%s'', " ") . '' "%s" %s''',
            \ 'sk' : ':GrepStop'},
        \ 'grepper' : {
            \ 'sr' : ':Grepper -noprompt -tool rg -query "%s" "%s" %s',
            \ 'sa' : ':Grepper -noprompt -tool rg -append -query "%s" "%s" %s',
            \ 'sk' : ':Grepper -stop'},
        \ 'sel' : {
            \ 'opt' : ['select rg engine'],
            \ 'lst' : ['asyncrun', 'grep', 'grepper'],
            \ 'cmd' : {sopt, arg -> extend(s:fw.engine, s:fw.rg[arg], 'force')},
            \ }
        \ },
    \ 'fuzzy' : {
        \ 'fzf' : {
            \ 'ff' : ':FzfFiles',
            \ 'fh' : ':FzfRg'},
        \ 'leaderf' : {
            \ 'ff' : ':LeaderfFile',
            \ 'fh' : ':Leaderf rg --nowrap'},
        \ 'sel' : {
            \ 'opt' : ['select fuzzy engine'],
            \ 'lst' : ['fzf', 'leaderf'],
            \ 'cmd' : {sopt, arg -> extend(s:fw.engine, s:fw.fuzzy[arg], 'force')},
        \ }
    \ },
    \ 'misc' : {
        \ 'markers' : ['.root', '.popc', '.git', '.svn'],
        \ 'strings' : [],
        \ },
    \ 'mappings' : []
    \ }
if IsVim()
    call extend(s:fw.engine, s:fw.rg.grep, 'force')
else
    call extend(s:fw.engine, s:fw.rg.grepper, 'force')
endif
call extend(s:fw.engine, s:fw.fuzzy.leaderf, 'force')

" s:fw.mappings {{{
let s:fw.mappings = [
                \  'fi',  'fbi',  'fti',  'foi',  'fpi',  'fri',  'fI',  'fbI',  'ftI',  'foI',  'fpI',  'frI',
                \  'fw',  'fbw',  'ftw',  'fow',  'fpw',  'frw',  'fW',  'fbW',  'ftW',  'foW',  'fpW',  'frW',
                \  'fs',  'fbs',  'fts',  'fos',  'fps',  'frs',  'fS',  'fbS',  'ftS',  'foS',  'fpS',  'frS',
                \  'f=',  'fb=',  'ft=',  'fo=',  'fp=',  'fr=',  'f=',  'fb=',  'ft=',  'fo=',  'fp=',  'fr=',
                \  'Fi',  'Fbi',  'Fti',  'Foi',  'Fpi',  'Fri',  'FI',  'FbI',  'FtI',  'FoI',  'FpI',  'FrI',
                \  'Fw',  'Fbw',  'Ftw',  'Fow',  'Fpw',  'Frw',  'FW',  'FbW',  'FtW',  'FoW',  'FpW',  'FrW',
                \  'Fs',  'Fbs',  'Fts',  'Fos',  'Fps',  'Frs',  'FS',  'FbS',  'FtS',  'FoS',  'FpS',  'FrS',
                \  'F=',  'Fb=',  'Ft=',  'Fo=',  'Fp=',  'Fr=',  'F=',  'Fb=',  'Ft=',  'Fo=',  'Fp=',  'Fr=',
                \ 'fli', 'flbi', 'flti', 'floi', 'flpi', 'flri', 'flI', 'flbI', 'fltI', 'floI', 'flpI', 'flrI',
                \ 'flw', 'flbw', 'fltw', 'flow', 'flpw', 'flrw', 'flW', 'flbW', 'fltW', 'floW', 'flpW', 'flrW',
                \ 'fls', 'flbs', 'flts', 'flos', 'flps', 'flrs', 'flS', 'flbS', 'fltS', 'floS', 'flpS', 'flrS',
                \ 'fl=', 'flb=', 'flt=', 'flo=', 'flp=', 'flr=', 'fl=', 'flb=', 'flt=', 'flo=', 'flp=', 'flr=',
                \ 'Fli', 'Flbi', 'Flti', 'Floi', 'Flpi', 'Flri', 'FlI', 'FlbI', 'FltI', 'FloI', 'FlpI', 'FlrI',
                \ 'Flw', 'Flbw', 'Fltw', 'Flow', 'Flpw', 'Flrw', 'FlW', 'FlbW', 'FltW', 'FloW', 'FlpW', 'FlrW',
                \ 'Fls', 'Flbs', 'Flts', 'Flos', 'Flps', 'Flrs', 'FlS', 'FlbS', 'FltS', 'FloS', 'FlpS', 'FlrS',
                \ 'Fl=', 'Flb=', 'Flt=', 'Flo=', 'Flp=', 'Flr=', 'Fl=', 'Flb=', 'Flt=', 'Flo=', 'Flp=', 'Flr=',
                \ 'fai', 'fabi', 'fati', 'faoi', 'fapi', 'fari', 'faI', 'fabI', 'fatI', 'faoI', 'fapI', 'farI',
                \ 'faw', 'fabw', 'fatw', 'faow', 'fapw', 'farw', 'faW', 'fabW', 'fatW', 'faoW', 'fapW', 'farW',
                \ 'fas', 'fabs', 'fats', 'faos', 'faps', 'fars', 'faS', 'fabS', 'fatS', 'faoS', 'fapS', 'farS',
                \ 'fa=', 'fab=', 'fat=', 'fao=', 'fap=', 'far=', 'fa=', 'fab=', 'fat=', 'fao=', 'fap=', 'far=',
                \ 'Fai', 'Fabi', 'Fati', 'Faoi', 'Fapi', 'Fari', 'FaI', 'FabI', 'FatI', 'FaoI', 'FapI', 'FarI',
                \ 'Faw', 'Fabw', 'Fatw', 'Faow', 'Fapw', 'Farw', 'FaW', 'FabW', 'FatW', 'FaoW', 'FapW', 'FarW',
                \ 'Fas', 'Fabs', 'Fats', 'Faos', 'Faps', 'Fars', 'FaS', 'FabS', 'FatS', 'FaoS', 'FapS', 'FarS',
                \ 'Fa=', 'Fab=', 'Fat=', 'Fao=', 'Fap=', 'Far=', 'Fa=', 'Fab=', 'Fat=', 'Fao=', 'Fap=', 'Far=',
                \ 'fvi', 'fvpi', 'fvI',  'fvpI',
                \ 'fvw', 'fvpw', 'fvW',  'fvpW',
                \ 'fvs', 'fvps', 'fvS',  'fvpS',
                \ 'fv=', 'fvp=', 'fv=',  'fvp=',
                \ ]
" }}}

" FUNCTION: s:fw.exec() dict {{{
function! s:fw.exec() dict
    let l:exec = printf(self.cmd, escape(self.pat, '"-#%\'), self.loc, self.opt)
    execute l:exec
    call FindWowHighlight(self.pat)
    call Plug_rpt_setExecution(l:exec)
endfunction
" }}}
" }}}

" FUNCTION: FindWow(keys, mode) {{{ è¶…é€ŸæŸ¥æ‰¾
function! FindWow(keys, mode)
    " doc
    " {{{
    " MapKeys: [fF][lav][btopr][IiWwSs=]
    "          [%1][%2 ][%3   ][4%     ]
    " Find: %1
    "   f : find working
    "   F : find working with inputing args
    " Command: %2
    "   '': find with rg by default
    "   l : find with rg in working root-filter and pass result to Leaderf
    "   a : find with rg append
    "   v : find with vimgrep
    " Location: %3
    "   b : find in current buffer(%)
    "   t : find in buffers of tab via popc
    "   o : find in buffers of all tabs via popc
    "   p : find with inputing path
    "   r : find with inputing working root and filter
    "  '' : find with working root-filter
    " Pattern: %4
    "   = : find text from clipboard
    "   Normal Mode: mode='n'
    "   i : find input
    "   w : find word
    "   s : find word with boundaries
    "   Visual Mode: mode='v'
    "   i : find input    with selected
    "   w : find visual   with selected
    "   s : find selected with boundaries
    "   LowerCase: [iws] find in ignorecase
    "   UpperCase: [IWS] find in case match
    " }}}
    " parse function
    " FUNCTION: s:parsePattern() closure {{{
    function! s:parsePattern() closure
        let l:pat = ''
        if a:mode ==# 'n'
            if a:keys =~? 'i'
                let l:pat = GetInput(' What to find: ')
            elseif a:keys =~? '[ws]'
                let l:pat = expand('<cword>')
            endif
        elseif a:mode ==# 'v'
            let l:selected = GetSelected()
            if a:keys =~? 'i'
                let l:pat = GetInput(' What to find: ', l:selected)
            elseif a:keys =~? '[ws]'
                let l:pat = l:selected
            endif
        endif
        if a:keys =~ '='
            let l:pat = getreg('+')
        endif
        return l:pat
    endfunction
    " }}}
    " FUNCTION: s:parseLocation() closure {{{
    function! s:parseLocation() closure
        let l:loc = ''
        if a:keys =~# 'b'
            let l:loc = expand('%:p')
        elseif a:keys =~# 't'
            let l:loc = join(popc#layer#buf#GetFiles('sigtab'), ' ')
        elseif a:keys =~# 'o'
            let l:loc = join(popc#layer#buf#GetFiles('alltab'), ' ')
        elseif a:keys =~# 'p'
            let l:loc = GetInput(' Where to find: ', '', 'customlist,GetMultiFilesCompletion', expand('%:p:h'))
        elseif a:keys =~# 'r'
            let l:loc = FindWowSetArgs('rf') ? s:fw.args.root : ''
        else
            if empty(s:fw.args.root)
                call FindWowRoot()
            endif
            if empty(s:fw.args.root)
                call FindWowSetArgs('r')
            endif
            let l:loc = s:fw.args.root
        endif
        return l:loc
    endfunction
    " }}}
    " FUNCTION: s:parseOptions() closure {{{
    function! s:parseOptions() closure
        let l:opt = ''
        if a:keys =~? 's'     | let l:opt .= '-w ' | endif
        if a:keys =~# '[iws]' | let l:opt .= '-i ' | elseif a:keys =~# '[IWS]' | let l:opt .= '-s ' | endif
        if a:keys !~# '[btop]'
            if !empty(s:fw.args.filters)
                let l:opt .= '-g "*.{' . s:fw.args.filters . '}" '
            endif
            if !empty(s:fw.args.globlst)
                let l:opt .= '-g ' . join(s:fw.args.globlst, ' -g ')
            endif
        endif
        if a:keys =~# 'F'
            let l:opt .= GetInput(' Args(-F, --hidden ...) to append: ')
        endif
        return l:opt
    endfunction
    " }}}
    " FUNCTION: s:parseCommand() closure {{{
    function! s:parseCommand() closure
        if a:keys =~# 'l'
            let l:cmd = s:fw.engine.sl
        elseif a:keys =~# 'a'
            let l:cmd = s:fw.engine.sa
        else
            let l:cmd = s:fw.engine.sr
            let s:fw.misc.strings = []
        endif
        return l:cmd
    endfunction
    " }}}
    " FUNCTION: s:parseVimgrep() closure {{{
    function! s:parseVimgrep() closure
        if a:keys !~# 'v'
            return 0
        endif

        " get pattern and set options
        let s:fw.pat = s:parsePattern()
        if empty(s:fw.pat) | return 0 | endif
        let l:pat = (a:keys =~? 's') ? ('\<' . s:fw.pat . '\>') : (s:fw.pat)
        let l:pat .= (a:keys =~# '[iws]') ? '\c' : '\C'

        " set loaction
        let l:loc = '%'
        if a:keys =~# 'p'
            let l:loc = GetInput(' Where to find: ', '', 'customlist,GetMultiFilesCompletion', expand('%:p:h'))
            if empty(l:loc) | return 0 | endif
        endif

        " execute vimgrep
        execute 'vimgrep /' . l:pat . '/j ' . l:loc
        echo 'Finding...'
        if empty(getqflist())
            echo 'No match: ' . l:pat
        else
            botright copen
            call FindWowHighlight(s:fw.pat)
        endif
        return 1
    endfunction
    " }}}

    " try use vimgrep first
    if s:parseVimgrep() | return | endif

    " find pattern
    let s:fw.pat = s:parsePattern()
    if empty(s:fw.pat) | return | endif

    " find location
    let s:fw.loc = s:parseLocation()
    if empty(s:fw.loc) | return | endif

    " find options
    let s:fw.opt = s:parseOptions()

    " find command
    let s:fw.cmd = s:parseCommand()

    " Find Working
    call s:fw.exec()
endfunction
" }}}

" FUNCTION: FindWowKill() {{{ åœæ­¢è¶…é€ŸæŸ¥æ‰¾
function! FindWowKill()
    execute s:fw.engine.sk
endfunction
" }}}

" FUNCTION: FindWowFuzzy(keys, r) {{{ æ¨¡ç³Šæœç´¢
" @param r: æ˜¯å¦è®¾ç½®æœç´¢ç›®å½•s:fw.args.root
function! FindWowFuzzy(keys, r)
    if !a:r && empty(s:fw.args.root)
        call FindWowRoot()
    endif
    if a:r || empty(s:fw.args.root)
        call FindWowSetArgs('r')
    endif
    if !empty(s:fw.args.root)
        execute 'lcd ' . s:fw.args.root
        execute s:fw.engine[a:keys]
    endif
endfunction
" }}}

" FUNCTION: FindWowSetEngine(type) {{{ è®¾ç½®engine
function! FindWowSetEngine(type)
    if a:type ==# 'engine'
        call PopSelection({
            \ 'opt' : ['select the engine'],
            \ 'lst' : ['rg', 'fuzzy'],
            \ 'sub' : {
                \ 'rg'    : s:fw.rg.sel,
                \ 'fuzzy' : s:fw.fuzzy.sel},
            \ 'cmd' : 'popset#set#SubPopSelection'
            \ })
    else
        call PopSelection(s:fw[a:type]['sel'])
    endif
endfunction
" }}}

" FUNCTION: FindWowRoot() {{{ æŸ¥æ‰¾rootè·¯å¾„
function! FindWowRoot()
    if empty(s:fw.misc.markers)
        return
    endif

    let l:dir = expand('%:p:h')
    let l:dir_last = ''
    while l:dir !=# l:dir_last
        let l:dir_last = l:dir
        for m in s:fw.misc.markers
            let l:root = l:dir . '/' . m
            if filereadable(l:root) || isdirectory(l:root)
                let s:fw.args.root = fnameescape(l:dir)
                return
            endif
        endfor
        let l:dir = fnamemodify(l:dir, ':p:h:h')
    endwhile
endfunction
" }}}

" FUNCTION: FindWowSetArgs() {{{ è®¾ç½®args
" @param type: r-root, f-filters, g-glob
" @return 0è¡¨ç¤ºå¼‚å¸¸ç»“æŸå‡½æ•°ï¼ˆrootæ— æ•ˆï¼‰ï¼Œ1è¡¨ç¤ºæ­£å¸¸ç»“æŸå‡½æ•°
function! FindWowSetArgs(type)
    if a:type =~# 'r'
        let l:root = GetInput(' Where (Root) to find: ', '', 'dir', expand('%:p:h'))
        if empty(l:root)
            return 0
        endif
        let l:root = fnamemodify(l:root, ':p')
        if l:root =~# '[/\\]$'
            let l:root = strcharpart(l:root, 0, strchars(l:root) - 1)
        endif
        let s:fw.args.root = l:root
    endif
    if a:type =~# 'f'
        let s:fw.args.filters = GetInput(' Which (Filter) to find: ')
    endif
    if a:type =~# 'g'
        let s:fw.args.globlst = split(GetInput(' What (Glob) to append: '), ',')
    endif
    return 1
endfunction
" }}}

" FUNCTION: FindWowGetArgs() {{{ è·å–args
function! FindWowGetArgs()
    if empty(s:fw.args.root)
        return []
    endif
    return [s:fw.args.root, s:fw.args.filters, s:fw.args.globlst]
endfunction
" }}}

" FUNCTION: FindWowHighlight([string]) {{{ é«˜äº®å­—ç¬¦ä¸²
" @param string: è‹¥æœ‰å­—ç¬¦ä¸²ï¼Œåˆ™å…ˆæ·»åŠ åˆ°s:fw.misc.stringsï¼Œå†é«˜äº®
function! FindWowHighlight(...)
    if &filetype ==# 'leaderf'
        " use leaderf's highlight
    elseif &filetype ==# 'qf'
        if a:0 >= 1
            call add(s:fw.misc.strings, escape(a:1, '/*'))
        endif
        for str in s:fw.misc.strings
            execute 'syntax match IncSearch /' . str . '/'
        endfor
    endif
endfunction
" }}}
" }}}

" output {{{
" FUNCTION: QuickfixBasic(kyes) {{{ åŸºæœ¬æ“ä½œ
function! QuickfixBasic(keys)
    let l:type = a:keys[0]
    let l:oprt = a:keys[1]
    if l:oprt == 'o'
        execute 'botright ' . l:type . 'open'
        call FindWowHighlight()
    elseif l:oprt == 'c'
        if &filetype==#'qf'
            wincmd p
        endif
        execute l:type . 'close'
    elseif l:oprt == 'j'
        execute l:type . 'next'
        silent! normal! zO
        normal! zz
    elseif l:oprt == 'k'
        execute l:type . 'previous'
        silent! normal! zO
        normal! zz
    endif
endfunction
" }}}

" FUNCTION: QuickfixGet() {{{ ç±»å‹ä¸ç¼–å·
function! QuickfixGet()
    " location-list : æ¯ä¸ªçª—å£å¯¹åº”ä¸€ä¸ªä½ç½®åˆ—è¡¨
    " quickfix      : æ•´ä¸ªvimå¯¹åº”ä¸€ä¸ªquickfix
    let l:type = ''
    let l:line = 0
    if &filetype ==# 'qf'
        let l:dict = getwininfo(win_getid())
        if l:dict[0].quickfix && !l:dict[0].loclist
            let l:type = 'c'
        elseif l:dict[0].quickfix && l:dict[0].loclist
            let l:type = 'l'
        endif
        let l:line = line('.')
    endif
    return [l:type, l:line]
endfunction
" }}}

" FUNCTION: QuickfixPreview() {{{ é¢„è§ˆ
function! QuickfixPreview()
    let [l:type, l:line] = QuickfixGet()
    if empty(l:type)
        return
    endif

    let l:last_winnr = winnr()
    execute l:type . 'rewind ' . l:line
    silent! normal! zO
    silent! normal! zz
    execute l:last_winnr . 'wincmd w'
endfunction
" }}}

" FUNCTION: QuickfixTabEdit() {{{ æ–°å»ºTabæ‰“å¼€çª—å£
function! QuickfixTabEdit()
    let [l:type, l:line] = QuickfixGet()
    if empty(l:type)
        return
    endif

    tabedit
    execute l:type . 'rewind ' . l:line
    silent! normal! zO
    silent! normal! zz
    execute 'botright ' . l:type . 'open'
    call FindWowHighlight()
endfunction
" }}}

" FUNCTION: QuickfixMakeIconv() {{{ ç¼–ç è½¬æ¢
function! QuickfixMakeIconv(sopt, argstr, type)
    let [l:from, l:to] = GetArgs(a:argstr)
    if a:type[0] ==# 'c'
        let l:list = getqflist()
        for t in l:list
            let t.text = iconv(t.text, l:from, l:to)
        endfor
        call setqflist(l:list)
    elseif a:type[0] ==# 'l'
        let l:list = getloclist(0)
        for t in l:list
            let t.text = iconv(t.text, l:from, l:to)
        endfor
        call setloclist(0, l:list)
    endif
endfunction
" }}}

" FUNCTION: QuickfixIconv() {{{ ç¼–ç è½¬æ¢
function! QuickfixIconv()
    let l:type = QuickfixGet()[0]
    if empty(l:type)
        return
    endif
    call PopSelection({
        \ 'opt' : ['Select encoding'],
        \ 'lst' : ['"cp936", "utf-8"', '"utf-8", "cp936"'],
        \ 'cmd' : 'QuickfixMakeIconv',
        \ 'arg' : [l:type,]
        \ })
endfunction
" }}}
" }}}

" option {{{
" Required: 'yehuohan/popset'

" s:opt {{{
let s:opt = {
    \ 'lst' : {
        \ 'conceallevel' : [2, 0],
        \ 'foldcolumn'   : [1, 0],
        \ 'virtualedit'  : ['all', ''],
        \ 'signcolumn'   : ['no', 'auto'],
        \ },
    \ 'func' : {
        \ 'number' : 'OptFuncNumber',
        \ 'syntax' : 'OptFuncSyntax',
        \ },
    \ }
" }}}

" FUNCTION: OptionInv(opt) {{{ åˆ‡æ¢å‚æ•°å€¼ï¼ˆboolå–åï¼‰
function! OptionInv(opt)
    execute printf('set inv%s', a:opt)
    execute printf('echo "%s = " . &%s', a:opt, a:opt)
endfunction
" }}}

" FUNCTION: OptionLst(opt) {{{ åˆ‡æ¢å‚æ•°å€¼ï¼ˆåˆ—è¡¨å¾ªç¯å–å€¼ï¼‰
function! OptionLst(opt)
    let l:lst = s:opt.lst[a:opt]
    let l:idx = index(l:lst, eval('&' . a:opt))
    let l:idx = (l:idx + 1) % len(l:lst)
    execute printf('set %s=%s', a:opt, l:lst[l:idx])
    execute printf('echo "%s = " . &%s', a:opt, a:opt)
endfunction
" }}}

" FUNCTION: OptionFunc(opt) {{{ åˆ‡æ¢å‚æ•°å€¼ï¼ˆå‡½æ•°å–å€¼ï¼‰
function! OptionFunc(opt)
    let Fn = function(s:opt.func[a:opt])
    call Fn()
endfunction
" }}}

" FUNCTION: OptFuncNumber() {{{ åˆ‡æ¢æ˜¾ç¤ºè¡Œå·
function! OptFuncNumber()
    if (&number) && (&relativenumber)
        set nonumber
        set norelativenumber
    elseif !(&number) && !(&relativenumber)
        set number
        set norelativenumber
    elseif (&number) && !(&relativenumber)
        set number
        set relativenumber
    endif
endfunction
" }}}

" FUNCTION: OptFuncSyntax() {{{ åˆ‡æ¢é«˜äº®
function! OptFuncSyntax()
    if exists('g:syntax_on')
        syntax off
        echo 'syntax off'
    else
        syntax on
        echo 'syntax on'
    endif
endfunction
" }}}
" }}}
" }}} End

" User Settings {{{
" basic {{{
    syntax on                           " è¯­æ³•é«˜äº®
    filetype plugin indent on           " æ‰“å¼€æ–‡ä»¶ç±»å‹æ£€æµ‹
    set number                          " æ˜¾ç¤ºè¡Œå·
    set relativenumber                  " æ˜¾ç¤ºç›¸å¯¹è¡Œå·
    set cursorline                      " é«˜äº®å½“å‰è¡Œ
    set cursorcolumn                    " é«˜äº®å½“å‰åˆ—
    set hlsearch                        " è®¾ç½®é«˜äº®æ˜¾ç¤ºæŸ¥æ‰¾åˆ°çš„æ–‡æœ¬
    set termguicolors                   " åœ¨ç»ˆç«¯ä¸­ä½¿ç”¨24ä½å½©è‰²
if IsVim()
    set renderoptions=                  " è®¾ç½®æ­£å¸¸æ˜¾ç¤ºunicodeå­—ç¬¦
endif
    set expandtab                       " å°†Tabç”¨Spaceä»£æ›¿ï¼Œæ–¹ä¾¿æ˜¾ç¤ºç¼©è¿›æ ‡è¯†indentLine
    set tabstop=4                       " è®¾ç½®Tabé”®å®½4ä¸ªç©ºæ ¼
    set softtabstop=4                   " è®¾ç½®æŒ‰<Tab>æˆ–<BS>ç§»åŠ¨çš„ç©ºæ ¼æ•°
    set shiftwidth=4                    " è®¾ç½®>å’Œ<å‘½ä»¤ç§»åŠ¨å®½åº¦ä¸º4
    set nowrap                          " é»˜è®¤å…³é—­æŠ˜è¡Œ
    set textwidth=0                     " å…³é—­è‡ªåŠ¨æ¢è¡Œ
    set listchars=eol:$,tab:Â»Â·,trail:~,space:.
                                        " ä¸å¯è§å­—ç¬¦æ˜¾ç¤º
    set autoindent                      " ä½¿ç”¨autoindentç¼©è¿›
    set nobreakindent                   " æŠ˜è¡Œæ—¶ä¸ç¼©è¿›
    set conceallevel=0                  " æ˜¾ç¤ºmarkdownç­‰æ ¼å¼ä¸­çš„éšè—å­—ç¬¦
    set foldenable                      " å……è®¸æŠ˜å 
    set foldcolumn=0                    " 0~12,æŠ˜å æ ‡è¯†åˆ—ï¼Œåˆ†åˆ«ç”¨â€œ-â€å’Œâ€œ+â€è€Œè¡¨ç¤ºæ‰“å¼€å’Œå…³é—­çš„æŠ˜å 
    set foldmethod=indent               " è®¾ç½®æŠ˜å ï¼Œé»˜è®¤ä¸ºç¼©è¿›æŠ˜å 
                                        " manual : æ‰‹å·¥å®šä¹‰æŠ˜å 
                                        " indent : æ›´å¤šçš„ç¼©è¿›è¡¨ç¤ºæ›´é«˜çº§åˆ«çš„æŠ˜å 
                                        " expr   : ç”¨è¡¨è¾¾å¼æ¥å®šä¹‰æŠ˜å 
                                        " syntax : ç”¨è¯­æ³•é«˜äº®æ¥å®šä¹‰æŠ˜å 
                                        " diff   : å¯¹æ²¡æœ‰æ›´æ”¹çš„æ–‡æœ¬è¿›è¡ŒæŠ˜å 
                                        " marker : å¯¹æ–‡ä¸­çš„æ ‡è®°æŠ˜å ï¼Œé»˜è®¤ä½¿ç”¨{{{,}}}æ ‡è®°
    set scrolloff=3                     " å…‰æ ‡ä¸Šä¸‹ä¿ç•™çš„è¡Œæ•°
    set laststatus=2                    " ä¸€ç›´æ˜¾ç¤ºçŠ¶æ€æ 
    set noshowmode                      " å‘½ä»¤è¡Œæ ä¸æ˜¾ç¤ºVISUALç­‰å­—æ ·
    set completeopt=menuone,preview     " è¡¥å…¨æ˜¾ç¤ºè®¾ç½®
    set backspace=2                     " Insertæ¨¡å¼ä¸‹ä½¿ç”¨BackSpaceåˆ é™¤
    set title                           " å…è®¸è®¾ç½®titlestring
    set hidden                          " å…è®¸åœ¨æœªä¿å­˜æ–‡ä»¶æ—¶åˆ‡æ¢buffer
    set bufhidden=                      " è·Ÿéšhiddenè®¾ç½®
    set nobackup                        " ä¸ç”Ÿæˆå¤‡ä»½æ–‡ä»¶
    set nowritebackup                   " è¦†ç›–æ–‡ä»¶å‰ï¼Œä¸ç”Ÿæˆå¤‡ä»½æ–‡ä»¶
    set autochdir                       " è‡ªåŠ¨åˆ‡æ¢å½“å‰ç›®å½•ä¸ºå½“å‰æ–‡ä»¶æ‰€åœ¨çš„ç›®å½•
    set noautowrite                     " ç¦æ­¢è‡ªåŠ¨ä¿å­˜æ–‡ä»¶
    set noautowriteall                  " ç¦æ­¢è‡ªåŠ¨ä¿å­˜æ–‡ä»¶
    set fileencodings=ucs-bom,utf-8,cp936,gb18030,big5,euc-jp,euc-kr,latin1
                                        " å°è¯•è§£ç åºåˆ—
    set fileformat=unix                 " ä»¥unixæ ¼å¼ä¿å­˜æ–‡æœ¬æ–‡ä»¶ï¼Œå³CRä½œä¸ºæ¢è¡Œç¬¦
    set magic                           " é»˜è®¤ä½¿ç”¨magicåŒ¹é…
    set ignorecase                      " ä¸åŒºåˆ«å¤§å°å†™æœç´¢
    set smartcase                       " æœ‰å¤§å†™å­—æ¯æ—¶æ‰åŒºåˆ«å¤§å°å†™æœç´¢
    set notildeop                       " ä½¿åˆ‡æ¢å¤§å°å†™çš„~ï¼Œç±»ä¼¼äºc,y,dç­‰æ“ä½œç¬¦
    set nrformats=bin,octal,hex,alpha   " CTRL-A-Xæ”¯æŒæ•°å­—å’Œå­—æ¯
    set noimdisable                     " åˆ‡æ¢Normalæ¨¡å¼æ—¶ï¼Œè‡ªåŠ¨æ¢æˆè‹±æ–‡è¾“å…¥æ³•
    set noerrorbells                    " å…³é—­é”™è¯¯ä¿¡æ¯å“é“ƒ
    set visualbell t_vb=                " å…³é—­å“é“ƒ(vb, visualbell)å’Œå¯è§†é—ªé“ƒ(t_vbï¼Œå³é—ªå±)ï¼Œå³normalæ¨¡å¼æ—¶æŒ‰escä¼šæœ‰å“é“ƒ
    set belloff=all                     " å…³é—­æ‰€æœ‰äº‹ä»¶çš„å“é“ƒ
    set helplang=cn,en                  " ä¼˜å…ˆæŸ¥æ‰¾ä¸­æ–‡å¸®åŠ©
if IsVim()
    if IsTermType('xterm') || IsTermType('xterm-256color')
        " ç»ˆç«¯å…‰æ ‡è®¾ç½®ï¼Œé€‚ç”¨äºurxvt,st,xterm,gnome-termial
        " 5,6: ç«–çº¿ï¼Œ  3,4: æ¨ªçº¿ï¼Œ  1,2: æ–¹å—
        let &t_SI = "\<Esc>[6 q"        " è¿›å…¥Insertæ¨¡å¼
        let &t_EI = "\<Esc>[2 q"        " é€€å‡ºInsertæ¨¡å¼
    endif
endif
" }}}

" gui {{{
let s:gui_fontsize = 12

" Gui-vim {{{
if IsGVim()
" FUNCTION: GuiAdjustFontSize(inc) {{{
function! GuiAdjustFontSize(inc)
    let s:gui_fontsize += a:inc
    if IsLinux()
        execute 'set guifont=DejaVu\ Sans\ Mono\ for\ Powerline\ ' . s:gui_fontsize
        execute 'set guifontwide=WenQuanYi\ Micro\ Hei\ Mono\ ' . s:gui_fontsize
    elseif IsWin()
        execute 'set guifont=Consolas_For_Powerline:h' . s:gui_fontsize . ':cANSI'
        execute 'set guifontwide=Microsoft_YaHei_Mono:h' . (s:gui_fontsize - 1). ':cGB2312'
    elseif IsMac()
        execute 'set guifont=DejaVu\ Sans\ Mono\ for\ Powerline:h' . s:gui_fontsize
    endif
endfunction
" }}}

    set guioptions-=m                   " éšè—èœå•æ 
    set guioptions-=T                   " éšè—å·¥å…·æ 
    set guioptions-=L                   " éšè—å·¦ä¾§æ»šåŠ¨æ¡
    set guioptions-=r                   " éšè—å³ä¾§æ»šåŠ¨æ¡
    set guioptions-=b                   " éšè—åº•éƒ¨æ»šåŠ¨æ¡
    set guioptions-=e                   " ä¸ä½¿ç”¨GUIæ ‡ç­¾
    call GuiAdjustFontSize(0)
    set lines=25
    set columns=90
    set linespace=0
    if IsWin()
        nnoremap <leader>tf <Esc>:call libcallnr('gvimfullscreen.dll', 'ToggleFullScreen', 0)<CR>
    endif
    nnoremap <kPlus> :call GuiAdjustFontSize(1)<CR>
    nnoremap <kMinus> :call GuiAdjustFontSize(-1)<CR>
endif
" }}}

" Gui-neovim {{{
if IsNVim()
augroup UserSettingsGui
    autocmd!
    autocmd VimEnter * call s:NVimQt_setGui()
augroup END

" FUNCTION: GuiAdjustFontSize(inc) {{{
function! GuiAdjustFontSize(inc)
    let s:gui_fontsize += a:inc
    if IsLinux()
        execute 'Guifont! WenQuanYi Micro Hei Mono:h' . s:gui_fontsize
        execute 'Guifont! DejaVu Sans Mono for Powerline:h' . s:gui_fontsize
    elseif IsWin()
        "Guifont! YaHei Mono For Powerline:h12
        "Guifont! Microsoft YaHei Mono:h12
        execute 'Guifont! Consolas For Powerline:h' . s:gui_fontsize
    endif
endfunction
" }}}

" FUNCTION: s:NVimQt_setGui() {{{
function! s:NVimQt_setGui()
if IsNVimQt()
    call GuiAdjustFontSize(0)
    GuiLinespace 0
    GuiTabline 0
    GuiPopupmenu 0
    " åŸºäºQt-Guiçš„è®¾ç½®
    nnoremap <leader>tf :call GuiWindowFullScreen(!g:GuiWindowFullScreen)<CR>
    nnoremap <leader>tm :call GuiWindowMaximized(!g:GuiWindowMaximized)<CR>
    nnoremap <kPlus> :call GuiAdjustFontSize(1)<CR>
    nnoremap <kMinus> :call GuiAdjustFontSize(-1)<CR>
endif
endfunction
" }}}
endif
" }}}
" }}}

" auto command {{{
augroup UserSettingsCmd
    "autocmd[!]  [group]  {event}     {pattern}  {nested}  {cmd}
    "autocmd              BufNewFile  *                    set fileformat=unix
    autocmd!

    autocmd BufNewFile *                set fileformat=unix
    autocmd BufRead,BufNewFile *.tex    set filetype=tex
    autocmd BufRead,BufNewFile *.tikz   set filetype=tex
    autocmd BufRead,BufNewFile *.gv     set filetype=dot

    autocmd Filetype vim        setlocal foldmethod=marker
    autocmd Filetype c          setlocal foldmethod=syntax
    autocmd Filetype cpp        setlocal foldmethod=syntax
    autocmd Filetype python     setlocal foldmethod=indent
    autocmd FileType go         setlocal expandtab
    autocmd FileType javascript setlocal foldmethod=syntax

    autocmd Filetype vim,help nnoremap <buffer> <S-k> :call MiscGotoKeyword('n')<CR>
    autocmd Filetype vim,help vnoremap <buffer> <S-k> :call MiscGotoKeyword('v')<CR>
augroup END
" }}}
" }}} End

" User Mappings {{{
" basic {{{
    " å›é€€æ“ä½œ
    nnoremap <S-u> <C-r>
    " å¤§å°å†™è½¬æ¢
    nnoremap <leader>u ~
    vnoremap <leader>u ~
    nnoremap <leader>gu g~
    " è¡Œç§»åŠ¨
    nnoremap > >>
    nnoremap < <<
    " åŠ å‡åºå·
    nnoremap <leader>aj <C-x>
    nnoremap <leader>ak <C-a>
    vnoremap <leader>aj <C-x>
    vnoremap <leader>ak <C-a>
    vnoremap <leader>agj g<C-x>
    vnoremap <leader>agk g<C-a>
    " åµŒå¥—æ˜ å°„åŒ¹é…ç¬¦(%)
if IsVim()
    packadd matchit
endif
    nmap <S-s> %
    vmap <S-s> %
    " è¡Œé¦–å’Œè¡Œå°¾
    nnoremap <S-l> $
    nnoremap <S-h> ^
    vnoremap <S-l> $
    vnoremap <S-h> ^
    " å¤åˆ¶åˆ°è¡Œé¦–è¡Œå°¾
    nnoremap yL y$
    nnoremap yH y^
    " j, k ç§»è¡Œ
    nnoremap j gj
    vnoremap j gj
    nnoremap k gk
    vnoremap k gk
    " æŠ˜å 
    nnoremap <leader>za zA
    nnoremap <leader>zc zC
    nnoremap <leader>zo zO
    nnoremap <leader>zm zM
    nnoremap <leader>zn zN
    nnoremap <leader>zr zR
    nnoremap <leader>zx zX
    " æ»šå±
    nnoremap <C-j> <C-e>
    nnoremap <C-k> <C-y>
    nnoremap zh zt
    nnoremap zl zb
    nnoremap <C-h> 2zh
    nnoremap <C-l> 2zl
    nnoremap <M-h> 16zh
    nnoremap <M-l> 16zl
    " å‘½ä»¤è¡Œç§»åŠ¨
    cnoremap <C-j> <Down>
    cnoremap <C-k> <Up>
    cnoremap <M-h> <Left>
    cnoremap <M-l> <Right>
    cnoremap <M-k> <C-Right>
    cnoremap <M-j> <C-Left>
    cnoremap <M-i> <C-B>
    cnoremap <M-o> <C-E>
    " HEXç¼–è¾‘
    nnoremap <leader>xx :%!xxd<CR>
    nnoremap <leader>xr :%!xxd -r<CR>
    " å‚æ•°è®¾ç½®
    nnoremap <leader>iw :call OptionInv('wrap')<CR>
    nnoremap <leader>il :call OptionInv('list')<CR>
    nnoremap <leader>ii :call OptionInv('ignorecase')<CR>
    nnoremap <leader>ie :call OptionInv('expandtab')<CR>
    nnoremap <leader>ib :call OptionInv('scrollbind')<CR>
    nnoremap <leader>iv :call OptionLst('virtualedit')<CR>
    nnoremap <leader>ic :call OptionLst('conceallevel')<CR>
    nnoremap <leader>if :call OptionLst('foldcolumn')<CR>
    nnoremap <leader>is :call OptionLst('signcolumn')<CR>
    nnoremap <leader>in :call OptionFunc('number')<CR>
    nnoremap <leader>ih :call OptionFunc('syntax')<CR>
    if IsLinux()
        inoremap <Esc> <Esc>:call MiscFcitx2en()<CR>
    endif
    nnoremap <leader>hl :call MiscHoldTopLine()<CR>
" }}}

" copy&paste {{{
    " yank & put
    vnoremap <leader>y ygv
    nnoremap ya :<C-U>execute 'let @0.=join(getline(line("."), line(".")+v:count), "\n")."\n"'<CR>
    nnoremap yd dd<Bar>:execute 'let @0.=@"'<CR>
    nnoremap <leader>p "0p
    nnoremap <leader>P "0P
    " ctrl-c & ctrl-v
    vnoremap <leader>c "+y
    vnoremap <C-c> "+y
    nnoremap <C-v> "+p
    inoremap <C-v> <Esc>"+pi
    inoremap <C-a> <C-v>
    " çŸ©å½¢é€‰æ‹©
    nnoremap vv <C-v>
    vnoremap vv <C-v>

    for t in split('q w e r t y u i o p a s d f g h j k l z x c v b n m', ' ')
        " å¯„å­˜å™¨å¿«é€Ÿå¤åˆ¶ä¸ç²˜è´´
        execute "vnoremap <leader>'" . t            .   ' "' . t . 'y'
        execute "nnoremap <leader>'" . t            .   ' "' . t . 'p'
        execute "nnoremap <leader>'" . toupper(t)   .   ' "' . t . 'P'
        " å¿«é€Ÿæ‰§è¡Œå®
        execute "nnoremap <leader>2" . t            .   ' @' . t
    endfor
    for t in split('1 2 3 4 5 6 7 8 9 0', ' ')
        execute "vnoremap <leader>'" . t            .   ' "' . t . 'y'
        execute "nnoremap <leader>'" . t            .   ' "' . t . 'p'
    endfor
" }}}

" tabs, buffers, quickfix, windows {{{
    " tabåˆ‡æ¢
    nnoremap <M-u> gT
    nnoremap <M-p> gt
    " bufferåˆ‡æ¢
    nnoremap <leader>bn :bnext<CR>
    nnoremap <leader>bp :bprevious<CR>
    nnoremap <leader>bl <C-^>
    " æ‰“å¼€/å…³é—­quickfix
    nnoremap <leader>qo :call QuickfixBasic('co')<CR>
    nnoremap <leader>qc :call QuickfixBasic('cc')<CR>
    nnoremap <leader>qj :call QuickfixBasic('cj')<CR>
    nnoremap <leader>qk :call QuickfixBasic('ck')<CR>
    " æ‰“å¼€/å…³é—­location-list
    nnoremap <leader>lo :call QuickfixBasic('lo')<CR>
    nnoremap <leader>lc :call QuickfixBasic('lc')<CR>
    nnoremap <leader>lj :call QuickfixBasic('lj')<CR>
    nnoremap <leader>lk :call QuickfixBasic('lk')<CR>
    " é¢„è§ˆquickfixå’Œlocation-list
    nnoremap <M-Space> :call QuickfixPreview()<CR>
    " åœ¨æ–°tabä¸­æ‰“å¼€åˆ—è¡¨é¡¹
    nnoremap <leader>qt :call QuickfixTabEdit()<CR>
    " å°†quickfixä¸­çš„å†…å®¹å¤åˆ¶location-list
    nnoremap <leader>ql :call setloclist(0, getqflist())<Bar>vertical botright lopen 35<CR>
    " ç¼–ç è½¬æ¢
    nnoremap <leader>qi :call QuickfixIconv()<CR>
    " åˆ†å‰²çª—å£
    nnoremap <leader>ws <C-w>s
    nnoremap <leader>wv <C-W>v
    nnoremap <leader>wc <C-w>c
    " ç§»åŠ¨ç„¦ç‚¹
    nnoremap <M-s> <C-w>h
    nnoremap <M-d> <C-w>j
    nnoremap <M-e> <C-w>k
    nnoremap <M-f> <C-w>l
    nnoremap <leader>wh <C-w>h
    nnoremap <leader>wj <C-w>j
    nnoremap <leader>wk <C-w>k
    nnoremap <leader>wl <C-w>l
    nnoremap <leader>wp <C-w>p
    nnoremap <leader>wP <C-w>P
    nnoremap <leader>ww <C-w>w
    " ç§»åŠ¨çª—å£
    nnoremap <leader>wH <C-w>H
    nnoremap <leader>wJ <C-w>J
    nnoremap <leader>wK <C-w>K
    nnoremap <leader>wL <C-w>L
    nnoremap <leader>wT <C-w>T
    " æ”¹å˜çª—å£å¤§å°
    nnoremap <leader>w= <C-w>=
    nnoremap <M-Up> :resize+5<CR>
    nnoremap <M-Down> :resize-5<CR>
    nnoremap <M-Left> :vertical resize-5<CR>
    nnoremap <M-Right> :vertical resize+5<CR>
" }}}

" file diff {{{
    " æ–‡ä»¶æ¯”è¾ƒï¼Œè‡ªåŠ¨è¡¥å…¨æ–‡ä»¶å’Œç›®å½•
    nnoremap <leader>ds :call FuncExecInput(['File: ', '', 'file', expand('%:p:h')], 'FuncDiffFile', 's')<CR>
    nnoremap <leader>dv :call FuncExecInput(['File: ', '', 'file', expand('%:p:h')], 'FuncDiffFile', 'v')<CR>
    " æ¯”è¾ƒå½“å‰æ–‡ä»¶ï¼ˆå·²ç»åˆ†å±ï¼‰
    nnoremap <leader>dt :diffthis<CR>
    " å…³é—­æ–‡ä»¶æ¯”è¾ƒï¼Œä¸diffthisäº’ä¸ºé€†å‘½ä»¤
    nnoremap <leader>do :diffoff<CR>
    " æ›´æ–°æ¯”è¾ƒç»“æœ
    nnoremap <leader>du :diffupdate<CR>
    " åº”ç”¨å·®å¼‚åˆ°åˆ«ä¸€æ–‡ä»¶ï¼Œ[range]<leader>dpï¼Œrangeé»˜è®¤ä¸º1è¡Œ
    nnoremap <leader>dp :<C-U>execute '.,+' . string(v:count1-1) . 'diffput'<CR>
    " æ‹‰å–å·®å¼‚åˆ°å½“å‰æ–‡ä»¶ï¼Œ[range]<leader>dgï¼Œrangeé»˜è®¤ä¸º1è¡Œ
    nnoremap <leader>dg :<C-U>execute '.,+' . string(v:count1-1) . 'diffget'<CR>
    " ä¸‹ä¸€ä¸ªdiff
    nnoremap <leader>dj ]c
    " å‰ä¸€ä¸ªdiff
    nnoremap <leader>dk [c
" }}}

" terminal {{{
if IsWin()
    nnoremap <leader>tz :terminal<CR>
else
    nnoremap <leader>tz :terminal zsh<CR>
endif
if IsVim()
    set termwinkey=<C-l>
    tnoremap <Esc> <C-l>N
else
    tnoremap <C-l> <C-\><C-n><C-w>
    tnoremap <Esc> <C-\><C-n>
endif
" }}}

" project {{{
    " åˆ›å»ºä¸´æ—¶æ–‡ä»¶
    nnoremap <leader>ei :call FuncExecInput(['Temp file suffix: '], 'FuncEditTempFile', 0)<CR>
    nnoremap <leader>eti :call FuncExecInput(['Temp file suffix: '], 'FuncEditTempFile', 1)<CR>
    for [key, val] in items({
            \ 'n' : '',
            \ 'c' : 'c',
            \ 'a' : 'cpp',
            \ 'p' : 'py',
            \ 'g' : 'go',
            \})
        execute 'nnoremap <leader>e'  . key . ' :call FuncEditTempFile("' . val . '", 0)<CR>'
        execute 'nnoremap <leader>et' . key . ' :call FuncEditTempFile("' . val . '", 1)<CR>'
    endfor
    nnoremap <leader>dh :call FuncDivideSpaceH()<CR>
    nnoremap <leader>dc :call FuncDivideSpaceC()<CR>
    nnoremap <leader>dl :call FuncDivideSpaceL()<CR>
    nnoremap <leader>dd :call FuncDivideSpaceD()<CR>
    nnoremap <leader>ae :call FuncAppendExecResult()<CR>
    nnoremap <leader>af :call FuncAppendCallResult()<CR>
    " ç¼–è¯‘è¿è¡Œå½“å‰æ–‡ä»¶æˆ–é¡¹ç›®
    nnoremap <leader>rf :call CompileFile('')<CR>
    nnoremap <leader>ri :call FuncExecInput(['Compile/Run args: ', '', 'customlist,GetMultiFilesCompletion', expand('%:p:h')], 'CompileFile')<CR>
    nnoremap <leader>rj :call CompileCell()<CR>
    nnoremap <leader>ra :call RcArg()<CR>
    nnoremap <leader>re :call RcExe()<CR>
    nnoremap <leader>rQ  :call RcQt()<CR>
    nnoremap <leader>rq  :call RcQtRun()<CR>
    nnoremap <leader>rcq :call RcQtClean()<CR>
    nnoremap <leader>rM  :call RcMake()<CR>
    nnoremap <leader>rm  :call RcMakeRun()<CR>
    nnoremap <leader>rcm :call RcMakeClean()<CR>
    nnoremap <leader>rV  :call RcVs()<CR>
    nnoremap <leader>rv  :call RcVsRun()<CR>
    nnoremap <leader>rcv :call RcVsClean()<CR>
    nnoremap <leader>rp  :call RcSphinx()<CR>
    nnoremap <leader>rP  :call RcSphinxRun()<CR>
    nnoremap <leader>rcp :call RcSphinxClean()<CR>
    " è°ƒè¯•
if IsVim()
    packadd termdebug
endif
" }}}

" find&search {{{
    " æ­£å‘æŸ¥æ‰¾
    vnoremap / "*y<Bar>:execute '/' . @*<CR>
    nnoremap <leader>/ :execute '/' . expand('<cword>')<CR>
    " FindWowæŸ¥æ‰¾
    for key in s:fw.mappings
        execute 'nnoremap <leader>' . key ':call FindWow("' . key . '", "n")<CR>'
    endfor
    for key in s:fw.mappings
        execute 'vnoremap <leader>' . key ':call FindWow("' . key . '", "v")<CR>'
    endfor
    nnoremap <leader>fk :call FindWowKill()<CR>
    nnoremap <leader>ff :call FindWowFuzzy('ff', 0)<CR>
    nnoremap <leader>fh :call FindWowFuzzy('fh', 0)<CR>
    nnoremap <leader>frf :call FindWowFuzzy('ff', 1)<CR>
    nnoremap <leader>frh :call FindWowFuzzy('fh', 1)<CR>
    nnoremap <leader>fee :call FindWowSetEngine('engine')<CR>
    nnoremap <leader>fes :call FindWowSetEngine('rg')<CR>
    nnoremap <leader>fez :call FindWowSetEngine('fuzzy')<CR>
    nnoremap <leader>fet :call FindWowRoot()<CR>
    nnoremap <leader>fea :call FindWowSetArgs('rfg')<CR>
    nnoremap <leader>fer :call FindWowSetArgs('r')<CR>
    nnoremap <leader>fef :call FindWowSetArgs('f')<CR>
    nnoremap <leader>feg :call FindWowSetArgs('g')<CR>
" }}}
" }}} End
